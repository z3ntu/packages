# Maintainer: Allan McRae <allan@archlinux.org>

# cross toolchain build order: binutils, gcc (pass 1), gnumach-headers, mig, hurd-headers,
#                              glibc (pass 1), libpthread, gcc (pass 2), glibc (pass 2)

_target=i686-pc-gnu
_sysroot=/usr/lib/cross-${_target}

pkgname=cross-${_target}-binutils
pkgver=2.20
pkgrel=1
pkgdesc="Hurd cross-toolchain binutils"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
depends=('glibc>=2.10.1' 'zlib')
options=('!libtool' '!distcc' '!ccache')
source=(http://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2)
md5sums=('ee2d3e996e9a2d669808713360fa96f8')

build() {
  cd $srcdir/binutils-$pkgver
  mkdir build && cd build

  ../configure --prefix=${_sysroot} --bindir=/usr/bin \
    --with-sysroot=${_sysroot} \
    --target=${_target} \
    --disable-nls
  make
  make DESTDIR=$pkgdir/ install
  
  # clean-up cross compiler root
  rm -r ${pkgdir}/${_sysroot}/share/{info,man}
  
  # needed for gcc build
  install -dm755 ${pkgdir}/${_sysroot}/include
}
