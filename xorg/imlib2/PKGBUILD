# Maintainer: Michael Walker <mike@barrucadu.co.uk>

pkgname=imlib2
pkgver=1.4.2
pkgrel=1
pkgdesc="Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support"
url="http://sourceforge.net/projects/enlightenment/"
arch=('i686')
license=('BSD')
depends=('libtiff' 'giflib' 'bzip2' 'freetype2' 'libxext' 'libpng' 'libid3tag' 'libjpeg')
options=('!libtool')
source=("http://downloads.sourceforge.net/enlightenment/$pkgname-$pkgver.tar.bz2" \
        'bufferoverflow.patch' libpng14.patch)
md5sums=('9f15568e76ebda9092c571e0df261f54' '76def1fe0ac2ac1686ff24d29979606f'\
         '68f671641e298ef7eead0aded4087c53')
sha1sums=('c3fb5de49e1e493544879f41262ec1ca525fc670' 'da84cea18ab852206e742f6e705665b4eb816405'\
         '43bb830f222f256154b42d0f1ca26938073a5603')

build() {
  cd $srcdir/$pkgname-$pkgver

  patch -Np1 -i $srcdir/libpng14.patch || return 1

  # Security Fix (Buffer Overflow), upstream fix
  patch -Np0 -i $srcdir/bufferoverflow.patch || return 1

  # Configure and Build
  ./configure --prefix= \
              --sysconfdir=/etc/imlib2 \
              --x-libraries=/lib \
              --disable-mmx || return 1
  make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install || return 1

  # Install License
  install -Dm644 COPYING $pkgdir/share/licenses/$pkgname/COPYING
}
