# Maintainer: Michael Walker <mike@barrucadu.co.uk>

pkgname=xorg-font-utils
pkgver=7.5
pkgrel=1
pkgdesc="X.Org font utilities"
arch=(i686)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('gcc-libs' 'sh' 'libxfont>=1.4.1' 'libfontenc>=1.0.5' 'xproto>=7.0.16')
makedepends=('bigreqsproto')
install=xorg-font-utils.install
source=(${url}/releases/individual/app/bdftopcf-1.0.2.tar.bz2
        ${url}/releases/individual/app/fonttosfnt-1.0.4.tar.bz2
        ${url}/releases/individual/app/mkfontdir-1.0.5.tar.bz2
        ${url}/releases/individual/app/mkfontscale-1.0.7.tar.bz2
        ${url}/releases/individual/font/font-util-1.1.1.tar.bz2
        http://people.redhat.com/yshao/ttmkfdir2.20021109.tar.bz2
        ttmkfdir.patch
        gcc-4.3.patch
        ttmkfdir2-pathmax.patch)
md5sums=('148f20d28caaa69bbe7dcca7c2674fb6'
         '07607b26fce3bdc2b204ff3bc43d3ac3'
         '9365ac66d19186eaf030482d312fca06'
         '96ca346f185c0ab48e42bf5bb0375da5'
         '5c735ae6916b65186f3c876d76c27ce3'
         '79e0401393c9728865aa73bb2bd68dd4'
         '099278628ad63631e872bc5590bade74'
         '92ef5f77a467cc188f8dec87b0fdb664'
         'c69f7b8ecf50d4b451ecbbce6aaf114a')

build() {
  cd "${srcdir}"

  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      if [ "${i}" = "ttmkfdir2" ]; then
        patch -Np0 -i "${srcdir}/ttmkfdir.patch" || return 1
        patch -Np1 -i "${srcdir}/ttmkfdir2-pathmax.patch" || return 1
        patch -Np1 -i "${srcdir}/gcc-4.3.patch" || return 1
        make || return 1
      else
        ./configure --prefix= --mandir=/share/man \
            --with-mapdir=/share/fonts/util --with-fontrootdir=/share/fonts || return 1
        make || return 1
      fi
      popd
    fi
  done
}

package() {
  cd "${srcdir}"

  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      if [ "${i}" = "ttmkfdir2" ]; then
        install -m755 -d "${pkgdir}/bin"
        install -m755 ttmkfdir "${pkgdir}/bin/" || return 1
        install -m755 -d "${pkgdir}/share/licenses/${pkgname}"
        echo "Unknown license" > "${pkgdir}/share/licenses/${pkgname}/LICENSE.ttmkfdir2" || return 1
      else
        make DESTDIR="${pkgdir}" install || return 1
        install -D -m644 COPYING "${pkgdir}"/share/licenses/${pkgname}/LICENSE.${i%-*} || return 1
      fi
      popd
    fi
  done
}
