#Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=xorg-util-macros
pkgver=1.6.1
pkgrel=1
pkgdesc="X.Org Autotools macros"
arch=(any)
license=('custom')
url="http://xorg.freedesktop.org/"
source=(${url}/releases/individual/util/util-macros-${pkgver}.tar.bz2)
sha1sums=('d6c3ed6f0c0deab9ee4f6d63f7b2c7ce3cbae280')

build() {
  cd "${srcdir}/util-macros-${pkgver}"
  ./configure --prefix= || return 1
  make || return 1
}

package() {
  cd "${srcdir}/util-macros-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/lib"
  mv "${pkgdir}/share/pkgconfig" "${pkgdir}/lib/" || return 1
  install -m755 -d "${pkgdir}/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/share/licenses/${pkgname}/" || return 1
}
