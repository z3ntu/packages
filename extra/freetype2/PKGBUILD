# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=freetype2
pkgver=2.4.2
pkgrel=1
pkgdesc="TrueType font rendering library"
arch=('i686')
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        freetype-2.3.0-enable-spr.patch
        freetype-2.2.1-enable-valid.patch)

build() {
  cd "${srcdir}/freetype-${pkgver}"
  patch -Np1 -i "${srcdir}/freetype-2.3.0-enable-spr.patch" 
  patch -Np1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch" 

  ./configure --prefix= 
  make 
}

package() {
  cd "${srcdir}/freetype-${pkgver}"
  make DESTDIR="${pkgdir}" install 
}
md5sums=('647ee8ed266f9a4117c8d0a4855b3d3e'
         '816dc8619a6904a7385769433c0a8653'
         '214119610444c9b02766ccee5e220680')
