# Maintainer: Michael Walker <mike@barrucadu.co.uk>

pkgname=git
pkgver=1.7.1
pkgrel=1
pkgdesc="The fast distributed version control system"
arch=(i686)
url="http://git-scm.com/"
license=('GPL2')
depends=('curl' 'expat>=2.0' 'perl-error' 'perl>=5.10.0')
makedepends=('python')
optdepends=('tk: gitk and git gui'
            'perl-libwww: git svn'
            'perl-term-readkey: git svn'
            'subversion: git svn'
            'cvsps: git cvsimport')
source=("http://kernel.org/pub/software/scm/git/${pkgname}-${pkgver}.tar.bz2" \
        "http://kernel.org/pub/software/scm/git/git-manpages-${pkgver}.tar.bz2")
md5sums=('3da231dbe82ad103373cb530ae7475d5'
         '50c3e1119a2197de208cbb69c7da0a50')

build() {
  cd $srcdir/$pkgname-$pkgver
  make prefix= gitexecdir=/lib/git-core
}

package() {
  cd $srcdir/$pkgname-$pkgver

  sed -i 's/ln "/cp "/' Makefile
  sed -i 's/ln "/cp "/' */Makefile

  make prefix= gitexecdir=/lib/git-core \
    NO_CROSS_DIRECTORY_HARDLINKS=1 \
    INSTALLDIRS=vendor DESTDIR=${pkgdir} install
  
  # bash completion
  mkdir -p $pkgdir/etc/bash_completion.d/
  install -m644 ./contrib/completion/git-completion.bash $pkgdir/etc/bash_completion.d/git

  # more contrib stuff
  cp -a ./contrib $pkgdir/share/git/

  # how 'bout some manpages?
  for mansect in man1 man5 man7; do
    for manpage in $srcdir/$mansect/*; do
      install -D -m644 $manpage $pkgdir/share/man/$mansect/$(basename $manpage)
    done
  done

  # remove perllocal.pod, .packlist, and empty directories.
  rm -rf $pkgdir/lib/perl5
}
