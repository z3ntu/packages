# Maintainer: Michael Walker <mike@barrucadu.co.uk>

pkgname=git
pkgver=1.7.2.2
pkgrel=1
pkgdesc="The fast distributed version control system"
arch=(i686)
url="http://git-scm.com/"
license=('GPL2')
depends=('curl' 'expat>=2.0' 'perl-error' 'perl>=5.10.0')
makedepends=('python')
optdepends=('tk: gitk and git gui'
            'perl-libwww: git svn'
            'perl-term-readkey: git svn'
            'subversion: git svn'
            'cvsps: git cvsimport')
source=("http://kernel.org/pub/software/scm/git/${pkgname}-${pkgver}.tar.bz2" \
        "http://kernel.org/pub/software/scm/git/git-manpages-${pkgver}.tar.bz2"
	git-daemon
	git-daemon.conf)
md5sums=('4a5840b6d650692cb320eddb5ccefbaf'
         '2278da6b57891bd9b789d313dc8eb772'
         'd777ff1a239b3d810dcf5d89f9d894af'
         '9d065134210aa0dd3f2b40d12d915040')

build() {
  cd $srcdir/$pkgname-$pkgver
  make prefix= gitexecdir=/lib/git-core
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make prefix= gitexecdir=/lib/git-core \
    NO_CROSS_DIRECTORY_HARDLINKS=1 \
    INSTALLDIRS=vendor DESTDIR=${pkgdir} install
  
  # bash completion
  mkdir -p $pkgdir/etc/bash_completion.d/
  install -m644 ./contrib/completion/git-completion.bash $pkgdir/etc/bash_completion.d/git

  # more contrib stuff
  cp -a ./contrib $pkgdir/share/git/

  # emacs interface
  mkdir -p $pkgdir/share/emacs/site-lisp
  mv $pkgdir/share/git/emacs $pkgdir/share/emacs/site-lisp/git
  rm $pkgdir/share/emacs/site-lisp/git/.gitignore

  # how 'bout some manpages?
  for mansect in man1 man5 man7; do
    for manpage in $srcdir/$mansect/*; do
      install -D -m644 $manpage $pkgdir/share/man/$mansect/$(basename $manpage)
    done
  done

  # remove perllocal.pod, .packlist, and empty directories.
  rm -rf $pkgdir/lib/perl5

  # git daemon script
  install -D -m755 $srcdir/git-daemon $pkgdir/etc/rc.d/git-daemon
  install -D -m644 $srcdir/git-daemon.conf $pkgdir/etc/conf.d/git-daemon.conf
}
