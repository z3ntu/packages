# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=python
pkgver=2.6.6
pkgrel=1
_pybasever=2.6
pkgdesc="A high-level scripting language"
arch=('i686')
license=('custom')
url="http://www.python.org"
depends=('db' 'bzip2' 'gdbm' 'openssl' 'zlib' 'expat' 'sqlite3')
makedepends=('tk')
optdepends=('tk: for IDLE, pynche and modulator')
provides=('python2' 'python-elementtree' 'python-ctypes')
source=(http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.bz2
	python-internal-expat.patch
  python-db-4.8.patch
	modulator-launcher
	pynche-launcher
  hurd-broken-poll.diff
  hurd-disable-nonworking-constants.diff)

build() {
  cd ${srcdir}/Python-${pkgver}

  patch -Np0 -i ${srcdir}/python-internal-expat.patch
  patch -Np0 -i ${srcdir}/python-db-4.8.patch  

  # Hurd 
  patch -p1 -i ${srcdir}/hurd-disable-nonworking-constants.diff 
  patch -p1 -i ${srcdir}/hurd-broken-poll.diff

  #http://bugs.python.org/issue6949

  export OPT="${CFLAGS}"
  ./configure --prefix= --enable-shared --with-threads \
    --enable-unicode 

  make  
}

package() {
  cd ${srcdir}/Python-${pkgver}
  
  make DESTDIR=${pkgdir} install 
  
  rm ${pkgdir}/bin/python
  ln -sf python${_pybasever} ${pkgdir}/bin/python
  ln -sf python${_pybasever} ${pkgdir}/bin/python2

  ln -sf ../../libpython${_pybasever}.so \
    ${pkgdir}/lib/python${_pybasever}/config/libpython${_pybasever}.so

  # Install the tools
  #modulator
  sed -i "s#%%PYBASEVER%%#${_pybasever}#" ${srcdir}/modulator-launcher
  install -m755 ${srcdir}/modulator-launcher ${pkgdir}/bin/modulator
  cp -r Tools/modulator ${pkgdir}/lib/python${_pybasever}/site-packages/

  #pynche
  sed -i "s#%%PYBASEVER%%#${_pybasever}#" ${srcdir}/pynche-launcher
  install -m755 ${srcdir}/pynche-launcher ${pkgdir}/bin/pynche
  rm -f Tools/pynche/*.pyw
  cp -r Tools/pynche ${pkgdir}/lib/python${_pybasever}/site-packages/

  #gettext
  install -m755 Tools/i18n/pygettext.py ${pkgdir}/bin/
  install -m755 Tools/i18n/msgfmt.py ${pkgdir}/bin/

  mkdir -p ${pkgdir}/lib/python${_pybasever}/Tools/scripts
  install -m644 Tools/scripts/README ${pkgdir}/lib/python${_pybasever}/Tools/scripts/
  install -m755 Tools/scripts/*py ${pkgdir}/lib/python${_pybasever}/Tools/scripts/

  # license
  install -Dm644 LICENSE ${pkgdir}/share/licenses/${pkgname}/LICENSE
}
md5sums=('cf4e6881bb84a7ce6089e4a307f71f14'
         '993800dbcc0c4f199c1ceee998a06e71'
         '10eb5449ead95b9cb13eae9f9a65894e'
         '793f25c6dfc74e75cadb72b2134f7c67'
         '108370afad19062abd2df4edaf060e57'
         'ecf1a0d5eddbcfe757034381152c8f66'
         '80088c2e94ad8bd419c78a63dc12d55b')
