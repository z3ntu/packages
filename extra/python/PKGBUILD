# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=python
pkgver=2.6.5
pkgrel=2
_pybasever=2.6
pkgdesc="A high-level scripting language"
arch=('i686')
license=('PSF')
url="http://www.python.org"
depends=('db' 'bzip2' 'gdbm' 'openssl' 'zlib' 'expat' 'sqlite3')
#makedepends=('tk>=8.5.0')
optdepends=('tk: for IDLE, pynche and modulator')
provides=('python2' 'python-elementtree' 'python-ctypes')
replaces=('python-elementtree' 'python-ctypes')
source=(http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.bz2
	python-2.6-internal-expat.patch
	python-2.6-db-4.8.patch
	modulator-launcher
	pynche-launcher
  hurd-broken-poll.dpatch
  hurd-disable-nonworking-constants.dpatch)

build() {
  cd ${srcdir}/Python-${pkgver}

  patch -Np0 -i ${srcdir}/python-2.6-internal-expat.patch

 #Hurd 
	patch -p0 < ../hurd-broken-poll.dpatch
	patch -p0 < ../hurd-disable-nonworking-constants.dpatch

  #http://bugs.python.org/issue6949
  patch -Np0 -i ${srcdir}/python-2.6-db-4.8.patch

  export OPT="${CFLAGS}"
  ./configure --prefix= --enable-shared --with-threads --enable-unicode

  make LN=cp || return 1
}

package() {
  cd ${srcdir}/Python-${pkgver}
  make LN=cp DESTDIR=${pkgdir} install || return 1
  
  rm ${pkgdir}/bin/python
  ln -sf python${_pybasever} ${pkgdir}/bin/python
  ln -sf python${_pybasever} ${pkgdir}/bin/python2

  ln -sf ../../libpython${_pybasever}.so \
    ${pkgdir}/lib/python${_pybasever}/config/libpython${_pybasever}.so

  # Install the tools
  #modulator
  sed -i "s#%%PYBASEVER%%#${_pybasever}#" ${srcdir}/modulator-launcher
  install -m755 ${srcdir}/modulator-launcher ${pkgdir}/bin/modulator
  cp -r Tools/modulator ${pkgdir}/lib/python${_pybasever}/site-packages/

  #pynche
  sed -i "s#%%PYBASEVER%%#${_pybasever}#" ${srcdir}/pynche-launcher
  install -m755 ${srcdir}/pynche-launcher ${pkgdir}/bin/pynche
  rm -f Tools/pynche/*.pyw
  cp -r Tools/pynche ${pkgdir}/lib/python${_pybasever}/site-packages/

  #gettext
  install -m755 Tools/i18n/pygettext.py ${pkgdir}/bin/
  install -m755 Tools/i18n/msgfmt.py ${pkgdir}/bin/

  mkdir -p ${pkgdir}/lib/python${_pybasever}/Tools/scripts
  install -m644 Tools/scripts/README ${pkgdir}/lib/python${_pybasever}/Tools/scripts/
  install -m755 Tools/scripts/*py ${pkgdir}/lib/python${_pybasever}/Tools/scripts/

  # license
  install -Dm644 LICENSE ${pkgdir}/share/licenses/${pkgname}/LICENSE
}
md5sums=('6bef0417e71a1a1737ccf5750420fdb3'
         '993800dbcc0c4f199c1ceee998a06e71'
         '10eb5449ead95b9cb13eae9f9a65894e'
         '793f25c6dfc74e75cadb72b2134f7c67'
         '108370afad19062abd2df4edaf060e57'
         '5dd3f2b420e525fbd5244db1bcb74d24'
         '922ffa3e9473909f3374ba0e0171c293')
