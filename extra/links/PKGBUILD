# Maintainer: Stephen Gilles <stephendgilles@gmail.com>

pkgname=links
pkgver=2.3
pkgrel=1
pkgdesc="A text WWW browser, similar to Lynx"
arch=('i686' 'x86_64')
url="http://links.twibright.com/"
license=('GPL')
depends=('bzip2' 'zlib' 'openssl')
makedepends=('libtiff' 'libpng' 'libxt')
optdepends=('libx11: for using xlinks' 'libtiff: for using xlinks' 'libpng: for using xlinks')
provides=('links-g')
conflicts=('links-g')
replaces=('links-g')
source=(http://links.twibright.com/download/${pkgname}-${pkgver}.tar.bz2 links.desktop)
md5sums=('1dc17862e766cec7c544166186e7167b'
         '7c47b15aaef51f1de129ff3a2afb7eac')
sha1sums=('0f91e550a80a2bc358ce3bdf48a23504a0582b02'
          'f600e27c2a71184444f7dd07a10230aa44463a02')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  (cd intl; ./gen-intl; ./synclang)
  ./configure --prefix= --mandir=/share/man --enable-javascript \
    --enable-graphics --with-x --with-fb
  make
  mv links xlinks

  ./configure --prefix= --mandir=/share/man --enable-javascript \
    --disable-graphics --without-x  --without-fb
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 xlinks "${pkgdir}/bin/xlinks"
  ln -s links.1.gz "${pkgdir}/share/man/man1/xlinks.1.gz"

  install -D -m644 "${srcdir}/links.desktop" "${pkgdir}/share/applications/links.desktop"
  install -d "${pkgdir}/share/pixmaps"
  install -m644 links_16x16_1.xpm links_16x16_2.xpm links_32x32.xpm "${pkgdir}/share/pixmaps/"

  install -d "${pkgdir}/share/doc/links/calibration"
  install -m644 doc/links_cal/* "${pkgdir}/share/doc/links/calibration/"
}
