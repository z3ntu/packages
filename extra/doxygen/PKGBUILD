# Maintainer: Stephen Gilles <stephendgilles@gmail.com>
# Contributor: Dan McGee <dan@archlinux.org>

pkgbase=doxygen
pkgname=('doxygen' 'doxygen-docs')
pkgver=1.7.4
pkgrel=1
license=('GPL')
arch=('i686')
url="http://www.doxygen.org/"
makedepends=('flex')
source=(ftp://ftp.stack.nl/pub/users/dimitri/${pkgname}-${pkgver}.src.tar.gz
	hurd_remove_-pipe.patch)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  if [ -e ${srcdir}/.patched ]; then
    msg "Already patched"
  else
    msg "Patching"
    patch -p1 -i ${srcdir}/hurd_remove_-pipe.patch
    touch ${srcdir}/.patched
  fi

  if [ -e ${srcdir}/.configured ]; then
    msg "Already configured"
  else
    msg "Configuring"
    QTDIR= ./configure --prefix /
    touch ${srcdir}/.configured
  fi

  make
}

package_doxygen(){
  pkgdesc="A documentation system for C++, C, Java, IDL and PHP"
  depends=('gcc-libs')
  optdepends=('graphviz: for caller/callee graph generation')

  cd ${srcdir}/${pkgname}-${pkgver}
  make INSTALL=${pkgdir} MAN1DIR=share/man/man1 install
}

package_doxygen-docs(){
  pkgdesc="Developer documentaion for doxygen"

  cd ${srcdir}/${pkgname}-${pkgver}
  make INSTALL=${pkgdir} MAN1DIR=share/man/man1 install_docs
}
md5sums=('ff908759ff7cd9464424b04ae6c68e48'
         'fea8037e7f4e433114229231391d01cb')
