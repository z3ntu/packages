# Maintainer: Stephen Gilles <stephendgilles@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=gtk2
pkgname=('gtk2' 'gtk-update-icon-cache')
pkgver=2.24.7
pkgrel=1
arch=('i686')
url="http://www.gtk.org/"
makedepends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage'
             'shared-mime-info' 'cairo' 'libcups' 'gdk-pixbuf2' 'gobject-introspection')
options=('!libtool' '!docs')
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-${pkgver}.tar.xz
        xid-collision-debug.patch)

build() {
  cd "${srcdir}/gtk+-${pkgver}"
  if [ -e ${srcdir}/.patched ] ; then
    msg2 "Already patched"
  else
    msg2 "Patching"
    patch -Np1 -i "${srcdir}/xid-collision-debug.patch"
    touch ${srcdir}/.patched
  fi

  export PYTHON=/bin/python2
  export PYTHONHOME=/

  if [ -e ${srcdir}/.configured ]; then
    msg2 "Already configured"
  else
    msg2 "Configuring"  
    CXX=/bin/false ./configure --prefix= \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --with-xinput=yes
    touch ${srcdir}/.configured
  fi

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make 
}

package_gtk2() {
    pkgdesc="The GTK+ Toolkit (v2)"
    install=gtk2.install
    depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
    backup=(etc/gtk-2.0/gtkrc)

    cd "${srcdir}/gtk+-${pkgver}"

  export PYTHON=/bin/python2
  export PYTHONHOME=/


    make DESTDIR="${pkgdir}" install
    sed -i "s#env python#env python2#" $pkgdir/bin/gtk-builder-convert
    echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc"
    #split this out to use with gtk3 too
    rm ${pkgdir}/bin/gtk-update-icon-cache
}
package_gtk-update-icon-cache() {
    pkgdesc="The GTK+ update icon cache tool"
    depends=('gdk-pixbuf2')

    cd "${srcdir}/gtk+-${pkgver}/gtk"

  export PYTHON=/bin/python2
  export PYTHONHOME=/


    install -D -m755 gtk-update-icon-cache ${pkgdir}/bin/gtk-update-icon-cache
}


md5sums=('719df30b5076a0e37b62f968a5e1312c'
         '22355df723f6ca358e30e6b8f0baace5')
