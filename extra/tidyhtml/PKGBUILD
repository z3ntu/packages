# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>
# $Id: PKGBUILD 69561 2010-02-21 04:47:38Z allan $
# Contributor: eric <eric@archlinux.org>
# Contributor: Markus Meissner <markus@meissna.de>

pkgname=tidyhtml
# obtain pkgver from http://tidy.cvs.sourceforge.net/tidy/tidy/src/version.h?view=markup
pkgver=1.46
pkgrel=1
pkgdesc="A tool to tidy down your HTML code to a clean style"
arch=('i686')
url="http://tidy.sourceforge.net/"
license=('custom')
options=('!libtool')
source=(ftp://ftp.archlinux.org/other/tidyhtml/${pkgname}-${pkgver}.tar.bz2
        hurd-utime.patch)

mksource() {
  mkdir tidyhtml-${pkgver}
  cd tidyhtml-${pkgver}
  cvs -d:pserver:anonymous@tidy.cvs.sourceforge.net:/cvsroot/tidy login   #hit enter for password
  cvs -z3 -d:pserver:anonymous@tidy.cvs.sourceforge.net:/cvsroot/tidy co -P tidy
  cd ..
  tar -cvjf tidyhtml-${pkgver}.tar.bz2 tidyhtml-${pkgver}/*
}

build(){
  cd $srcdir/${pkgname}-${pkgver}/tidy
  # Fix for hurd
  patch -p1 -i "${srcdir}/hurd-utime.patch"

  source build/gnuauto/setup.sh
  
  ./configure --prefix=
  make
}

package() { 
  cd $srcdir/${pkgname}-${pkgver}/tidy
  make DESTDIR=$pkgdir install
  
  install -Dm644 htmldoc/license.html \
    ${pkgdir}/share/licenses/${pkgname}/license.txt
}
