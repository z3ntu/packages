# Maintainer: Matt Windsor <hayashi@archhurd.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=sdl
pkgver=1.2.14
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard"
arch=('i686')
url="http://www.libsdl.org"
license=('LGPL')
depends=('glibc' 'libxext' 'libxrender' 'libx11')
makedepends=('mesa')
options=('!libtool')
source=(http://www.libsdl.org/release/SDL-${pkgver}.tar.gz
        sdl-1.2.14-joystick-crash.diff
        sdl-1.2.14-fix-mouse-clicking.patch
        sdl-1.2.14-fix-disappearing-cursor.patch)
md5sums=('e52086d1b508fa0b76c52ee30b55bec4'
         '9d8890b3817736a5d365f7497f096634'
         '04d8c179f125e04bcd4c9d60e013c2d7'
         'a6cf3e71b653aa97d0d8ae6c0a789807')

build() {
  cd ${srcdir}/SDL-${pkgver}
  patch -Np1 -i $srcdir/sdl-1.2.14-joystick-crash.diff
  patch -Np1 -i $srcdir/sdl-1.2.14-fix-mouse-clicking.patch
  patch -Np1 -i $srcdir/sdl-1.2.14-fix-disappearing-cursor.patch

  # TODO eventually: enable sound when it appears on the Hurd.

  ./configure --prefix= --disable-nasm --disable-alsa --disable-esd \
              --with-x --disable-rpath
  make
}

package() {
  cd ${srcdir}/SDL-${pkgver}
  make DESTDIR=${pkgdir} install
}
