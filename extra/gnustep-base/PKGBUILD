# Maintainer: Michael Walker <mike@barrucadu.co.uk>

pkgname=gnustep-base
pkgver=1.20.1
pkgrel=1
pkgdesc="The GNUstep base package"
arch=('i686')
url="http://www.gnustep.org/"
license=("GPL" "LGPL")
depends=(libxslt gmp gcc-libs openssl ffcall gnustep-make gnutls)
makedepends=(gcc-objc gnustep-make)
groups=('gnustep-core')
options=('!emptydirs' '!makeflags')
source=(ftp://ftp.gnustep.org/pub/gnustep/core/$pkgname-$pkgver.tar.gz)
md5sums=('8ade7c698159df7a43c7b5052321e4e0')

build() {
  cd $srcdir/$pkgname-$pkgver

  source /etc/profile.d/GNUstep.sh

  # need this stupid hack
  mkdir -p $pkgdir/opt/GNUstep/System/Library/Libraries/Resources/gnustep-base/NSTimeZones

  ./configure --prefix=/opt/GNUstep --enable-ffcall --disable-libffi --disable-avahi

  # fix file ownership
  sed -i 's/tar -xf $(TIMEZONE_ARCHIVE);/tar -xf $(TIMEZONE_ARCHIVE);chown -R root:root * ;/' NSTimeZones/Makefile.postamble

  make VERBOSE=1
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install
  chown -R root.root $pkgdir/
}
