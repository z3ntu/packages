# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=tcl
pkgver=8.5.9
pkgrel=1
pkgdesc="The Tcl scripting language"
arch=('i686')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=('glibc')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz)

build() {
  cd ${srcdir}/tcl${pkgver}/unix
  
  ./configure --prefix= --mandir=/share/man \
        --enable-threads --disable-64bit
  make 
}

package() {
  cd ${srcdir}/tcl${pkgver}/unix
  make INSTALL_ROOT=${pkgdir} install install-private-headers
  ln -sf tclsh8.5 ${pkgdir}/bin/tclsh
  # install license
  install -Dm644 ../license.terms ${pkgdir}/share/licenses/${pkgname}/LICENSE

  # remove buildroot traces / fixes #3602
  sed -i \
  -e "s,^TCL_BUILD_LIB_SPEC='-L.*/unix,TCL_BUILD_LIB_SPEC='-L/lib," \
  -e "s,^TCL_SRC_DIR='.*',TCL_SRC_DIR='/include'," \
  -e "s,^TCL_BUILD_STUB_LIB_SPEC='-L.*/unix,TCL_BUILD_STUB_LIB_SPEC='-L/lib," \
  -e "s,^TCL_BUILD_STUB_LIB_PATH='.*/unix,TCL_BUILD_STUB_LIB_PATH='/lib," \
  -e "s,^TCL_LIB_FILE='libtcl8.5..TCL_DBGX..so',TCL_LIB_FILE=\"libtcl8.5\$\{TCL_DBGX\}.so\"," \
  -e "s,^TCL_CC_SEARCH_FLAGS='\(.*\)',TCL_CC_SEARCH_FLAGS='\1:/lib'," \
  -e "s,^TCL_LD_SEARCH_FLAGS='\(.*\)',TCL_LD_SEARCH_FLAGS='\1:/lib'," \
  ${pkgdir}/lib/tclConfig.sh
}
md5sums=('8512d8db3233041dd68a81476906012a')
