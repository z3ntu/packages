# Maintainer: Stephen Gilles <stephendgilles@gmail.com>
# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=libdrm
pkgver=2.4.26
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('i686')
license=('custom')
depends=('glibc')
makedepends=(libpciaccess)
options=('!libtool')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        no-pthread-stubs.patch
	01-hurd-fix-ioccom-location.patch
        COPYING)
     
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/no-pthread-stubs.patch
  patch -Np1 -i ${srcdir}/01-hurd-fix-ioccom-location.patch

  autoreconf --force --install

  ./configure --prefix= \
      --disable-intel  \
      --disable-radeon \
      --disable-vmwgfx-experimental-api \
      --disable-nouveau-experimental-api

  #when this goes x86_64, check to make sure /machine/ is still correct
  CFLAGS="$CFLAGS -I/include/mach/machine" make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/share/licenses/${pkgname}/" || return 1
}
md5sums=('062569426773f69b11a47a7712bba770'
         'c722c8406507b7e3a8da7a3030d1d9cf'
         '754eb937d2f71f889f00eb61753f2f58'
         'ba65e71c481b94ef0fb6c23c7f21ffa1')
