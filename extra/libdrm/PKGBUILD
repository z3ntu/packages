# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=libdrm
pkgver=2.4.20
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('i686')
license=('custom')
depends=('glibc')
options=('!libtool')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        no-pthread-stubs.patch
        COPYING
        intel-use-the-correct-size-when-allocating-reloc_target_info-array.patch)
md5sums=('3c56e03172b236e14905ef9a68ba2f97'
         '9b5ec3bbebe86921e2057694c42f65b8'
         'ba65e71c481b94ef0fb6c23c7f21ffa1'
         '2dd7d84d3ebbc23c92b602104fae5df3')
     
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/no-pthread-stubs.patch" || return 1
  patch -p1 -i "${srcdir}/intel-use-the-correct-size-when-allocating-reloc_target_info-array.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix= \
      --enable-intel --enable-radeon \
      --enable-vmwgfx-experimental-api \
			--enable-nouveau-experimental-api || return 1

  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/share/licenses/${pkgname}/" || return 1
}
