# Maintainer: Stephen Gilles <stephendgilles@gmail.com> 
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fontforge
pkgver=20110222
pkgrel=1
pkgdesc='Outline and bitmap font editor'
arch=('i686') 
url='http://fontforge.sourceforge.net'
license=('BSD')
depends=('libxkbui' 'libxi' 'libxml2' 'pango' 'giflib' 'libtiff' 'python2')
options=('!libtool' '!makeflags')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}_full-${pkgver}.tar.bz2")
sha1sums=('8fada07647f102351bb1d7d1c4da487356e7142f')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i 's/python /python2 /g' Makefile.dynamic.in
	export CFLAGS="${CFLAGS// -O2 / -O1 }" # on i686, -O2 yields FS#20430
	./configure \
		--prefix= \
		--mandir=/share/man \
		--enable-type3 \
		--enable-devicetables \
		--with-regular-link \
		--with-python=python2 \
		--enable-pyextension \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/share/licenses/${pkgname}/LICENSE"
}
