# Maintainer: Michael Walker <mike@barrucadu.co.uk>

pkgname=perlxml
_realname=XML-Parser
pkgver=2.36
pkgrel=1
pkgdesc="XML::Parser - an XML parser module for perl"
arch=(i686)
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl' 'expat')
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/M/MS/MSERGEANT/${_realname}-${pkgver}.tar.gz)
md5sums=('1b868962b658bd87e1563ecd56498ded')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make MAN1EXT=1p MAN3EXT=3pm
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  make install MAN1EXT=1p MAN3EXT=3pm DESTDIR="${pkgdir}"

  # remove perllocal.pod and .packlist.
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}
