# Maintainer: Stephen Gilles <stephendgilles@gmail.com>

pkgname=unzip
pkgver=6.0
pkgrel=1
pkgdesc="Unpacks .zip archives such as those made by PKZIP"
arch=('i686')
url="http://www.info-zip.org/"
license=('custom')
depends=('bzip2' 'bash')
source=('http://downloads.sourceforge.net/infozip/unzip60.tar.gz')
md5sums=('62b490407489521db863b523a7f86375')

build() {
  cd ${srcdir}/${pkgname}${pkgver/./}

  # set CFLAGS -- from Debian
  export CFLAGS="$CFLAGS -D_FILE_OFFSET_BITS=64 -DACORN_FTYPE_NFS \
  -DWILD_STOP_AT_DIR -DLARGE_FILE_SUPPORT -DUNICODE_SUPPORT \
  -DUNICODE_WCHAR -DUTF8_MAYBE_NATIVE -DNO_LCHMOD -DDATE_FORMAT=DF_YMD \
  -DUSE_BZIP2 -DNATIVE"

  # make -- from Debian
  make -f unix/Makefile LOCAL_UNZIP="$CFLAGS" prefix= LF2="" \
  D_USE_BZ2=-DUSE_BZIP2 L_BZ2=-lbz2 unzips || return 1

  # install -- from Debian
  make -f unix/Makefile prefix=${pkgdir} INSTALL_PROGRAM="install" install || return 1

  # install the license file
  install -Dm644 LICENSE ${pkgdir}/share/licenses/unzip/LICENSE || return 1

  # fix manpage location
  mkdir -p ${pkgdir}/share || return 1
  mv ${pkgdir}/man ${pkgdir}/share/ || return 1
}

