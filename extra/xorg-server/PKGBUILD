# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=xorg-server
pkgver=1.8.0
pkgrel=1
pkgdesc="X.Org X servers"
arch=('i686')
license=('custom')
url="http://xorg.freedesktop.org"
depends=('libgl' 'libdrm' 'libxfont' 'libgcrypt' 'libpciaccess' 'libxv' 'pixman' 'xcursor-themes' 'xkeyboard-config' 'xorg-server-utils' 'xorg-fonts-misc' 'xbitmaps' 'diffutils' 'inputproto')
makedepends=('libx11' 'mesa' 'xf86driproto' 'xtrans' 'libxkbfile' 'randrproto' 'renderproto' 'xcmiscproto' 'bigreqsproto' 'resourceproto' 'videoproto' 'compositeproto' 'scrnsaverproto' 'xf86dgaproto' 'libgl' 'glproto' 'xorg-util-macros' 'xineramaproto' 'recordproto')
conflicts=('catalyst-utils<=9.2' 'xf86-input-calcomp' 'xf86-input-citron' 'xf86-input-digitaledge' 'xf86-input-dmc' 'xf86-input-dynapro' 'xf86-input-elo2300'
	'xf86-input-jamstudio' 'xf86-input-magellan' 'xf86-input-magictouch' 'xf86-input-microtouch' 'xf86-input-palmax' 'xf86-input-spaceorb' 'xf86-input-summa' 'xf86-input-tek4957' 'xf86-input-ur98' 'xf86-video-vga' 'xf86-video-intel-legacy' 'nvidia-96xx-utils<96.43.14' 'nvidia-173xx-utils<173.14.21')
options=('!libtool')
provides=('x-server')
groups=('xorg')
install=xorg-server.install
backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
source=(${url}/releases/individual/xserver/${pkgname}-${pkgver}.tar.bz2
        xorg-redhat-die-ugly-pattern-die-die-die.patch
        xvfb-run
        xvfb-run.1
        10-evdev.conf
        10-quirks.conf)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Get rid of the ugly pattern
  patch -Np3 -i "${srcdir}/xorg-redhat-die-ugly-pattern-die-die-die.patch" || return  1

  sed -i -e 's/\$(prefix)\/etc/\/etc/g' config/Makefile.* || return 1

  ./configure --prefix= \
      --enable-ipv6 \
      --enable-dri \
      --disable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-install-setuid \
      --disable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-default-font-path=/share/fonts/misc,/share/fonts/100dpi:unscaled,/share/fonts/75dpi:unscaled,/share/fonts/TTF,/share/fonts/Type1 \
      --with-xkb-path=/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
			--with-dri-driver-path=/lib/xorg/modules/dri  || return 1

  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make LINK=cp DESTDIR="${pkgdir}" install || return 1

  install -m755 "${srcdir}/xvfb-run" "${pkgdir}/bin/" || return 1
  install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/share/man/man1/" || return 1

  rm -rf "${pkgdir}/var/log" || return 1

  install -m755 -d "${pkgdir}/var/lib/xkb" || return 1
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/10-evdev.conf" "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/xorg.conf.d/" || return 1

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/lib/xorg/modules/extensions/libglx.xorg" || return 1

  install -m755 -d "${pkgdir}/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/share/licenses/${pkgname}/" || return 1
}
md5sums=('7cec3a11890bb53f4a07854319360348'
         '1a336eb22e27cbf443ec5a2ecddfa93c'
         '52fd3effd80d7bc6c1660d4ecf23d31c'
         '376c70308715cd2643f7bff936d9934b'
         'e930e153d69968a795fe4e2ef5d9a5dc'
         'd4f7dfc6be4ef4e2c6dd7632a9d88abe')
