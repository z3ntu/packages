# Maintainer: Michael Walker <mike@barrucadu.co.uk>

pkgname=giflib
pkgver=4.1.6
pkgrel=1
pkgdesc="A library for reading and writing gif images"
url="http://sourceforge.net/projects/giflib/"
arch=('i686')
license=('MIT')
depends=('libx11' 'libsm')
provides=("libungif=${pkgver}")
conflicts=('libungif')
replaces=('libungif')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/giflib/${pkgname}-${pkgver}.tar.bz2)
md5sums=('7125644155ae6ad33dbc9fc15a14735f')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
  install -m755 -d ${pkgdir}/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/share/licenses/${pkgname}/

  # libungif compatibility - instructions taken from Redhat specfile
  MAJOR=`echo ${pkgver} | sed 's/\([0-9]\+\)\..*/\1/'`
  gcc -shared -Wl,-soname,libungif.so.${MAJOR} -Llib/.libs -lgif -o libungif.so.${pkgver}
  install -m755 libungif.so.${pkgver} ${pkgdir}/lib/
  ln -sf libungif.so.${pkgver} ${pkgdir}/lib/libungif.so.4
  ln -sf libungif.so.4 ${pkgdir}/lib/libungif.so
}
