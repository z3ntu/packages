# Maintainer: Stephen Gilles <stephendgilles@gmail.com>
# Contributor: Alexander Preisinger <alexander.preisinger@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>
#
pkgname=dbus
pkgver=1.4.14
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686)
license=('GPL' 'custom')
depends=("dbus-core>=${pkgver}" 'libx11')
makedepends=('libsm' 'libice')
source=(http://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz
        30-dbus)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ECHO="echo" ./configure --prefix= --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/lib/dbus-1.0 --with-dbus-user=81 \
      --with-system-pid-file=/var/run/dbus.pid \
      --disable-verbose-mode --disable-static \
      --enable-inotify --disable-dnotify \
      --disable-tests --disable-asserts
  make -C tools dbus-launch
  install -m755 -d "${pkgdir}/bin"
  install -m755 -d "${pkgdir}/share/man/man1"
  install -m755 tools/dbus-launch "${pkgdir}/bin/"
  install -m644 doc/dbus-launch.1 "${pkgdir}/share/man/man1/"

  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
  install -m755 "${srcdir}/30-dbus" "${pkgdir}/etc/X11/xinit/xinitrc.d/"

  install -d -m755 "${pkgdir}/share/licenses/dbus"
  install -m644 COPYING "${pkgdir}/share/licenses/dbus/"
}
md5sums=('ae6de2562a57516cfabaf56903375ba9'
         'd14e59575f04e55d21a04907b6fd9f3c')
