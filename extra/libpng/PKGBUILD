# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=libpng
pkgver=1.4.2
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${pkgver}-apng.patch.gz")
md5sums=('d22ef5878842d918d6d110c0e7cc3afc'
         '378ade7c68c25d00e099b28911d06561')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -p1 -i "${srcdir}/libpng-${pkgver}-apng.patch"

  libtoolize --force --copy
  aclocal
  autoconf
  automake --add-missing

  ./configure --prefix=

  cd contrib/pngminus
  make PNGLIB="-L${pkgdir}/lib -lpng" -f makefile.std png2pnm pnm2png
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make ECHO=echo DESTDIR="${pkgdir}" install
 
  cd contrib/pngminus
  install -m755 png2pnm pnm2png "${pkgdir}/bin/"
  install -D -m644 ../../LICENSE "${pkgdir}/share/licenses/${pkgname}/LICENSE"
}
