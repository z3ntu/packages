# Maintainer: Michael Walker <mike@barrucadu.co.uk>

pkgname=libpng
pkgver=1.4.8
_apngver=1.4.7
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.xz
        http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${_apngver}-apng.patch.gz)
md5sums=('2ce595d571f2b06a9403ed5bcfa4ecbd'
         '4842a4cba3487a64084ca0a7417ff1c7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -p1 -i "${srcdir}/libpng-${_apngver}-apng.patch"

  ./configure --prefix=
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  cd contrib/pngminus
  make PNGLIB="-L${pkgdir}/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "${pkgdir}/bin/"
  install -D -m644 ../../LICENSE "${pkgdir}/share/licenses/${pkgname}/LICENSE"
}
