# Maintainer:  Michael Walker <mike@barrucadu.co.uk>
# Contributor: Alexander Preisinger <alexander.preisinger@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gdb
pkgver=7.2
pkgrel=1
pkgdesc="The GNU Debugger"
arch=('i686')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('ncurses' 'expat' 'python')
makedepends=('texinfo')
backup=('etc/gdb/gdbinit')
options=('!libtool')
install=gdb.install
source=( http://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.bz2 )

md5sums=('64260e6c56979ee750a01055f16091a5')


build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  # doesn't work with system readline >= 6
  ./configure --prefix= \
    --with-system-gdbinit=/etc/gdb/gdbinit \
    --disable-nls --build=i686-pc-gnu 

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # install "custom" system gdbinit
  install -dm755 $pkgdir/etc/gdb
  touch $pkgdir/etc/gdb/gdbinit

  # resolve conflicts with binutils
  rm -f ${pkgdir}/include/{bfd,dis-asm,symcat,ansidecl,bfdlink}.h
  rm -f ${pkgdir}/lib/{libiberty,libbfd,libopcodes}.a
  rm -f ${pkgdir}/share/info/{bfd.info,configure.info,standards.info}
}

