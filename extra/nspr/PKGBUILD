# Maintainer: Stephen Gilles <stephendgilles@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
pkgname=nspr
pkgver=4.8.9
pkgrel=1
pkgdesc="Netscape Portable Runtime"
arch=('i686')
url="http://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
depends=('glibc')
makedepends=('zip')
options=(!emptydirs)
source=(ftp://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/${pkgname}-${pkgver}.tar.gz
        nspr.pc.in
	01-hurd-fix-thread-error.patch
	02-hurd-fix-rng-status.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  if [ "$CARCH" = "x86_64" ]; then
    confflags="--enable-64bit"
  else
    confflags=""
  fi
  
  if [ -e ${srcdir}/.patched ]; then
    msg "Already patched"
  else
    msg "Patching"
    sed -e 's/\$(MKSHLIB) \$(OBJS)/\$(MKSHLIB) \$(LDFLAGS) \$(OBJS)/g' \
        -i mozilla/nsprpub/config/rules.mk
    patch -Np1 -i ${srcdir}/01-hurd-fix-thread-error.patch
    patch -Np1 -i ${srcdir}/02-hurd-fix-rng-status.patch
    touch ${srcdir}/.patched
  fi

  ./mozilla/nsprpub/configure \
      --prefix= \
      --libdir=/lib \
      --includedir=/include/nspr \
      --enable-optimize \
      --disable-debug ${confflags}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  NSPR_LIBS=`./config/nspr-config --libs`
  NSPR_CFLAGS=`./config/nspr-config --cflags`
  NSPR_VERSION=`./config/nspr-config --version`
  install -m755 -d "${pkgdir}/lib/pkgconfig"
  sed "${srcdir}/nspr.pc.in" -e "s,%libdir%,/lib," \
  	-e "s,%prefix%,," \
	-e "s,%exec_prefix%,/bin," \
	-e "s,%includedir%,/include/nspr," \
	-e "s,%NSPR_VERSION%,${NSPR_VERSION}," \
	-e "s,%FULL_NSPR_LIBS%,${NSPR_LIBS}," \
	-e "s,%FULL_NSPR_CFLAGS%,${NSPR_CFLAGS}," > "${pkgdir}/lib/pkgconfig/nspr.pc"
  chmod 644 "${pkgdir}/lib/pkgconfig/nspr.pc"
  ln -sf nspr.pc "${pkgdir}/lib/pkgconfig/mozilla-nspr.pc"

  chmod 644 ${pkgdir}/lib/*.a

  rm -rf "${pkgdir}/bin/compile-et.pl" \
	"${pkgdir}/bin/prerr.properties" \
	"${pkgdir}/share/aclocal/nspr.m4" \
	"${pkgdir}/include/nspr/md"
}
md5sums=('60770d45dc08c0f181b22cdfce5be3e8'
         'bce1611f3117b53fc904cab549c09967'
         '959547cfdca3b44ff812bec32fb923ce'
         '354348c82d46eda9711e606ff5692b69')
