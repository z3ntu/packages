# Maintainer: Stephen Gilles <stephendgilles@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: FJ <joostef@gmail.com>

pkgname=libwmf
pkgver=0.2.8.4
pkgrel=1
pkgdesc="A library for reading vector images in Microsoft's native Windows Metafile Format (WMF)"
arch=('i686')
url="http://wvware.sourceforge.net/libwmf.html"
license=("LGPL")
depends=('libpng' 'libx11' 'freetype2' 'libjpeg' 'gsfonts' 'expat')
makedepends=('gtk2' 'libxt' 'pkg-config')
options=('!libtool' '!docs' '!emptydirs')
install=libwmf.install
source=(http://downloads.sourceforge.net/sourceforge/wvware/${pkgname}-${pkgver}.tar.gz
	libpng14.patch libwmf-0.2.8.4-useafterfree.patch)
md5sums=('d1177739bf1ceb07f57421f0cee191e0' '33d3c07a1be9f99fd88de220930c5550'\
         '0f02c1cb8a87bf7158634503bf0537e2')
sha1sums=('822ab3bd0f5e8f39ad732f2774a8e9f18fc91e89' '15edb94eec7c9cea988e83660b334e56bf1fe897'\
         'ea6d28880840e86c96f9079bfd591da54dcffa5c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/libpng14.patch"
  patch -Np1 -i "${srcdir}/libwmf-0.2.8.4-useafterfree.patch"
  ./configure --prefix= \
              --with-gsfontdir=/share/fonts/Type1 \
	      --with-fontdir=/share/fonts/Type1 \
	      --with-gsfontmap=/share/ghostscript/8.15/lib/Fontmap.GS
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  #Remove fonts, these are in gsfonts
  rm -rf "${pkgdir}/share/fonts"
  #Remove static GTK loader, can't use it anyways
  rm -f "${pkgdir}"/lib/gtk-2.0/*/loaders/io-wmf.a
}
