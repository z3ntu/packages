# Maintainer: Michael Walker <mike@barrucadu.co.uk>

pkgname=libjpeg
pkgver=8.0.2
_pkgver=8b
pkgrel=1
pkgdesc="Library of JPEG support functions"
arch=('i686')
url="http://www.ijg.org/"
license=('custom')
depends=('glibc')
makedepends=('libtool')
options=('!libtool')
source=(http://www.ijg.org/files/jpegsrc.v${_pkgver}.tar.gz)
md5sums=('e022acbc5b36cd2cb70785f5b575661e')

build() {
  cd $srcdir/jpeg-${_pkgver}
  ./configure --prefix= --enable-shared --enable-static
  make
}

package() {
  cd $srcdir/jpeg-${_pkgver}
  make prefix=$pkgdir mandir=$pkgdir/share/man install
  
  # See FS#11116 - re-assess current need sometime...
  install -m644 jpegint.h $pkgdir/include

  install -Dm644 README $pkgdir/share/licenses/libjpeg/README
}
