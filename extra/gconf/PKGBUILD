# Maintainer: Stephen Gilles <stephendgilles@gmail.com>
# Contributor:  Jan de Groot <jan@archlinux.org>

pkgname=gconf
pkgver=3.2.0
pkgrel=1
pkgdesc="A configuration database system"
arch=('i686')
license=('LGPL')
depends=('libxml2' 'polkit' 'libldap' 'dbus-glib' 'gtk3')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection')
options=('!libtool')
install=gconf.install
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/GConf/3.2/GConf-${pkgver}.tar.xz
        gconf-merge-schema
        gconfpkg
        gconf-reload.patch
        01_xml-gettext-domain.patch
	02_path_max.patch)

build() {
  cd "${srcdir}/GConf-${pkgver}"

  if [ -e ${srcdir}/.patched ]; then
    msg "Already patched"
  else
    msg "Patching"
    # Patch from fedora - reloads gconf after installing schemas
    patch -Np1 -i "${srcdir}/gconf-reload.patch"
    # http://bugzilla.gnome.org/show_bug.cgi?id=568845
    patch -Np1 -i "${srcdir}/01_xml-gettext-domain.patch"
    # Obligatory PATH_MAX issues
    patch -Np1 -i "${srcdir}/02_path_max.patch"
    touch ${srcdir}/.patched
  fi

  # Python2 fix
  sed -i '1s|env python$|&2|' gsettings/gsettings-schema-convert

  export PYTHON=/bin/python2
  export PYTHONHOME=/
  export CFLAGS="$CFLAGS -I/lib/glib-2.0/include/"
  
  if [ -e ${srcdir}/.configured]; then
    msg "Already configured"
  else
    msg "Configuring"
    ./configure --prefix= --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/lib/GConf \
      --disable-static --enable-defaults-service --with-gtk=3.0 \
      --disable-orbit
    touch ${srcdir}/.configured
  fi

  make pkglibdir=/lib/GConf
}

package() {
  cd "${srcdir}/GConf-${pkgver}"

  export PYTHON=/bin/python2
  export PYTHONHOME=/
  
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/gconf/gconf.xml.system"
  install -m755 "${srcdir}/gconf-merge-schema" "${pkgdir}/bin/"
  install -Dm755 "${srcdir}/gconfpkg" "${pkgdir}/sbin/gconfpkg"
}


md5sums=('33bb4d86e67cd5aacba089eb752e3f3c'
         '240b473fa17b748fc9020caf372d936c'
         '85626a7e41fd58a47a8d822539ad209d'
         'e1336e83080dda4d6c1497bb00f0b26f'
         '1f9362976d71312bdb57c57fa031681d'
         'cc22de4168f3de936cf0a5fa680f82ce')

