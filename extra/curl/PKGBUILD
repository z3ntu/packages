# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=curl
pkgver=7.20.0
pkgrel=1
pkgdesc="An URL retrival utility and library"
arch=('i686')
url="http://curl.haxx.se"
license=('MIT')
depends=('zlib' 'openssl' 'bash')
options=('!libtool')
source=("http://curl.haxx.se/download/${pkgname}-${pkgver}.tar.bz2"
        'm4-typo.patch')
md5sums=('3dda78c4a808d9a779dc3a2ae81b47d8'
         '4e2737ddc209d2d1990890d3a9dc7dcf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/m4-typo.patch || return 1
  ./configure \
  	--with-random=/dev/urandom \
	--prefix= \
	--mandir=/share/man \
	--disable-dependency-tracking \
	--enable-ipv6 \
	--disable-ldaps \
	--disable-ldap \
	--enable-manual \
	--enable-versioned-symbols \
	--with-ca-path=/etc/ssl/certs \
	--without-libidn || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 COPYING "${pkgdir}/share/licenses/${pkgname}/COPYING" || return 1
  install -D -m644 docs/libcurl/libcurl.m4 "${pkgdir}/share/aclocal/libcurl.m4" || return 1
}
