# Maintainer: Stephen Gilles <stephendgilles@gmail.com>
# Contributor: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=curl
pkgver=7.21.7
pkgrel=1
pkgdesc="An URL retrival utility and library"
arch=('i686')
url="http://curl.haxx.se"
license=('MIT')
depends=('ca-certificates' 'zlib' 'openssl' 'bash')
options=('!libtool')
source=("http://curl.haxx.se/download/${pkgname}-${pkgver}.tar.bz2")


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
  	--with-random=/dev/urandom \
	--prefix= \
	--mandir=/share/man \
	--disable-dependency-tracking \
	--enable-ipv6 \
	--disable-ldaps \
	--disable-ldap \
	--enable-manual \
	--enable-versioned-symbols \
	--with-ca-path=/etc/ssl/certs \
	--without-libidn
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/share/licenses/${pkgname}/COPYING"
  install -D -m644 docs/libcurl/libcurl.m4 "${pkgdir}/share/aclocal/libcurl.m4"

  # upstream Arch Linux at this point replaces curlbuild.h with a file that seems to be designed to allow cross-arch compiling. As 32-bit only, we don't need this.
}
md5sums=('5f6d50c4d4ee38c57fe37e3cff75adbd')
