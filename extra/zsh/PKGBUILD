# Maintainer: Stephen Gilles <stephendgilles@gmail.com>
# Contributor: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=zsh
pkgver=4.3.12
pkgrel=1
pkgdesc="A very advanced and programmable command interpreter (shell) for UNIX"
arch=('i686')
url="http://www.zsh.org/"
license=('custom')
depends=('ncurses' 'pcre')
install=zsh.install
source=(ftp://ftp.zsh.org/pub/${pkgname}-${pkgver}.tar.bz2)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  if [ -e ${srcdir}/.configured ]; then
    msg "Already configured"
  else
    msg "Configuring"

    # FS#16360
    sed -i "s:init.d:rc.d:g" Doc/Zsh/compsys.yo \
                           Doc/zsh.texi \
                           Completion/Unix/Type/_services \
                           Completion/Unix/Command/_init_d
    ./configure --prefix= \
              --bindir=/bin \
              --enable-etcdir=/etc/zsh \
	      --enable-zshenv=/etc/zsh/zshenv \
              --enable-zlogin=/etc/zsh/zlogin \
              --enable-zlogout=/etc/zsh/zlogout \
              --enable-zprofile=/etc/profile \
              --enable-zshrc=/etc/zsh/zshrc \
              --enable-maildir-support \
	      --with-term-lib="ncursesw" \
	      --enable-multibyte \
	      --enable-function-subdirs \
	      --enable-zsh-mem \
	      --with-tcsetpgrp \
              --enable-pcre \
              --enable-zsh-secure-free \
	      --disable-gdbm
    touch ${srcdir}/.configured
  fi
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  install -D -m644 LICENCE "${pkgdir}/share/licenses/${pkgname}/LICENSE"
}

md5sums=('7a82c0bf0635e046ca4e9ec1bdef3811')
