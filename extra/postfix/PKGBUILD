# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>
# $Id: PKGBUILD 82352 2010-06-10 12:06:11Z paul $
# Contributor: Jeff Brodnax <tullyarcher@bellsouth.net>
# Maintainer: Paul Mattal <paul@archlinux.org>
pkgname=postfix
pkgver=2.7.1
pkgrel=1
pkgdesc="Secure, fast, easy to administer drop in replacement for Sendmail (MTA)"
arch=('i686')
license=('custom')
depends=('pcre' 'libsasl' 'libmysqlclient' 'postgresql-libs>=8.4' 'libldap>=2.4' 'db')
backup=(etc/postfix/aliases etc/postfix/virtual etc/postfix/relocated \
	etc/postfix/access etc/postfix/header_checks etc/postfix/transport \
	etc/postfix/generic etc/postfix/canonical \
	etc/postfix/main.cf etc/postfix/master.cf)
install="${pkgname}.install"
provides=('smtp-server' 'smtp-forwarder')
url="http://www.postfix.org/"
source=(ftp://ftp.porcupine.org/mirrors/postfix-release/official/${pkgname}-${pkgver}.tar.gz 
        ${pkgname}.patch.bz2 
      	${pkgname})

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	
	make makefiles \
	        CCARGS="-DUSE_SASL_AUTH -I/include/sasl \
			-DUSE_CYRUS_SASL \
			-DHAS_LDAP \
			-DUSE_TLS \
			-DHAS_MYSQL -I/include/mysql \
			-DHAS_PGSQL -I/include/postgresql" \
	        AUXLIBS="-lsasl2 -lssl -lcrypto -lldap -llber -lmysqlclient -lz -lm -lpq"
	make OPT="${CFLAGS}"
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	sh postfix-install -non-interactive \
		install_root="${pkgdir}/" \
		daemon_directory="/lib/${pkgname}" \
		command_directory="/sbin" \
		mailq_directory="/bin" \
		newaliases_directory="/bin" \
		sample_directory="/etc/${pkgname}/sample" \
		manpage_directory="/share/man"

	cd ${pkgdir}
	cat ${srcdir}/${pkgname}.patch |patch -Np0 || return 1
	rm -f etc/${pkgname}/main.cf~

	cd ${pkgdir}
	mkdir etc/rc.d
	install -m 0755 ${srcdir}/${pkgname} etc/rc.d/${pkgname}

	install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE ${pkgdir}/share/licenses/${pkgname}/LICENSE
}
md5sums=('b7a5c3ccd309156a65d6f8d2683d4fa1'
         'a3c45ff23ef036143711793fcf2478c3'
         '0ce12689d8147800f5c7323601669ddb')
