# Maintainer: Michael Walker <mike@barrucadu.co.uk>

pkgname=w3m
pkgver=0.5.3
pkgrel=1
pkgdesc='A pager/text-based WWW browser'
arch=('i686')
url="http://w3m.sourceforge.net/"
license=('custom')
depends=('openssl' 'gc' 'ncurses' 'zlib')
makedepends=('imlib2')
optdepends=('imlib2: for graphics support')
source=("http://downloads.sourceforge.net/sourceforge/w3m/$pkgname-$pkgver.tar.gz")
sha1sums=('444b6c8cf7094ee95f8e9de96b37f814b9d83237')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix= \
    --libexecdir=/lib \
    --enable-ansi-color \
    --enable-image=x11 \
    --with-imagelib=imlib2 \
    --with-termlib=ncurses \
    --disable-mouse \
    --disable-w3mmailer
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
  install -d "$pkgdir"/share/{doc,licenses}/"$pkgname"
  find doc/* | grep -v CVS | xargs -i install "{}" "$pkgdir/share/doc/$pkgname/"
  ln -s "../../doc/$pkgname/README" "$pkgdir/share/licenses/$pkgname/"
}
