# Maintainer: Michael Walker <mike@barrucadu.co.uk>

pkgname=libtiff
pkgver=3.9.2
pkgrel=1
pkgdesc="Library for manipulation of TIFF images"
arch=('i686')
url="http://www.libtiff.org"
license=('custom')
depends=('libjpeg' 'zlib')
makedepends=('libgl' 'freeglut' 'libxmu' 'libxi')
optdepends=('freeglut: for using tiffgt')
options=('!libtool')
source=(http://download.osgeo.org/libtiff/tiff-${pkgver}.tar.gz \
        libtiff-CVE-2009-2285.patch tiff-3.9.0-CVE-2009-2347.patch)
md5sums=('93e56e421679c591de7552db13384cb8' 'ff61077408727a82281f77a94f555e2a'\
         '867fa3ec83e748abf247f9706ef13f9a')
sha1sums=('5c054d31e350e53102221b7760c3700cf70b4327' 'eadce8c8bd72ea9c74f35300bf299131813b0c8b'\
         'cf8b68ed8f218d2429ab296c2033dc0292efe299')

build() {
  cd "${srcdir}/tiff-${pkgver}"
  patch -p1 < ../libtiff-CVE-2009-2285.patch
  patch -p0 < ../tiff-3.9.0-CVE-2009-2347.patch
  ./configure --prefix= --sysconfdir=/etc --mandir=/share/man
  make
}

package() {
  cd "${srcdir}/tiff-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYRIGHT "${pkgdir}/share/licenses/${pkgname}/LICENSE"
}
