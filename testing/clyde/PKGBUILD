# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
pkgname=clyde
pkgver=20110206
pkgrel=1
pkgdesc="Next-generation libalpm/makepkg wrapper."
arch=('i686')
url="http://clyde.archuser.com"
license=('custom')
depends=('lua-lzlib' 'lua-yajl-git' 'luasocket' 'luafilesystem')
makedepends=('git')
provides=('clyde' 'lualpm' 'lualpm-git')
conflicts=('clyde' 'lualpm' 'lualpm-git')

source=('remove-prctl.patch')
md5sums=('c00fffb0570841881332d3196a74fb96')

_gitroot="git://github.com/Kiwi/clyde.git"
_gitname="clyde"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."
  
  # Change out Archlinux for Archhurd
  sed -i 's|archlinux|archhurd|g' ${srcdir}/$_gitname/clydelib/sync.lua
  sed -i 's|archlinux|archhurd|g' ${srcdir}/$_gitname/clydelib/aur.lua


  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # Handle absense of sys/prctl.h
  patch -p1 < ${srcdir}/remove-prctl.patch    

  make lualpm clyde
  make DESTDIR=${pkgdir} install_lualpm install_clyde
  install -Dm644 LICENSE ${pkgdir}/share/licenses/${pkgname}/LICENSE
} 
