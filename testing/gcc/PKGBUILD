# Maintainer: Matthias Lanzinger <mlaenz@gmail.com>

pkgbase=('gcc')
pkgname=('gcc' 'gcc-libs' 'gcc-fortran' 'gcc-objc')
pkgver=4.5.1
pkgrel=1
_libstdcppmanver=20100312       # Note: check source directory name when updating this
pkgdesc="The GNU Compiler Collection"
arch=('i686')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL')
makedepends=('binutils' 'libmpc' 'elfutils' 'cloog-ppl')
options=('!libtool' '!emptydirs')
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-{core,g++,fortran,objc,ada}-${pkgver}.tar.bz2
        ftp://gcc.gnu.org/pub/gcc/libstdc++/doxygen/libstdc++-man.${_libstdcppmanver}.tar.bz2
        gcc-4.5.0_hurd.diff)


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in
  
  #patch for succesfull compile on hurd
  patch -Np1 < ${srcdir}/gcc-4.5.0_hurd.diff
  
  cd libdecnumber && ln -sf dpd no; cd ..

  cd ..
  rm -rf build
  mkdir build
  cd build
  ../${pkgname}-${pkgver}/configure --build=$CHOST --prefix= \
    --with-cloog --with-ppl= --with-gmp \
    --enable-long-long --enable-c99 \
    --enable-clocale=gnu --enable-gnu-unique-object \
    --enable-lto --enable-plugin \
    --enable-shared --enable-threads=posix --enable-__cxa_atexit \
    --enable-languages=c,c++,fortran,objc,obj-c++ --disable-libstdcxx-pch \
    --disable-multilib --mandir=/share/man --infodir=/share/info \
    --libexecdir=/lib 

  make || return 1
} 

package_gcc() { 
  groups=('base-devel')
  depends=('binutils' 'libmpc' 'elfutils' 'cloog-ppl' 'sh')
  install=gcc.install
  
  cd ${srcdir}/build

  #install and clean up
  make -j1 DESTDIR=${pkgdir} install
  rm $pkgdir/bin/*gfortran
  rm $pkgdir/lib/gcc/$CHOST/${pkgver}/{finclude,include/objc}/*
  rm $pkgdir/lib/gcc/$CHOST/${pkgver}/{cc1obj{,plus},f951,libgfortranbegin.a}
 
  rm $pkgdir/lib/{*.so*,lib{gfortran,objc}.a}
  rm $pkgdir/share/info/{libgomp,gfortran}.info
  rm $pkgdir/share/locale/{de,fr}/LC_MESSAGES/libstdc++.mo
  rm $pkgdir/share/man/man1/gfortran.1

  #sometimes required
  install -dm755 ${pkgdir}/lib
  ln -sf /bin/cpp ${pkgdir}/lib/cpp
  ln -sf gcc ${pkgdir}/bin/cc
  ln -sf g++ ${pkgdir}/bin/c++
  
  # install the libstdc++ man pages
  install -dm755 ${pkgdir}/share/man/man3
  install -m644 ${srcdir}/libstdc++-man.${_libstdcppmanver}/man3/* \
    ${pkgdir}/share/man/man3/
  # deal with conflicts...
  rm -f ${pkgdir}/share/man/man3/{ctime,queue,random,regex,string}.3

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING.RUNTIME \
    ${pkgdir}/share/licenses/gcc/RUNTIME.LIBRARY.EXCEPTION
}


package_gcc-libs() {
  pkgdesc="Runtime libraries shipped by GCC for C and C++ languages"
  groups=('base')
  depends=('glibc' 'sh')
  install=gcc-libs.install

  
  cd ${srcdir}/build  
  make -j1 -C $CHOST/libgcc DESTDIR=${pkgdir} install-shared
  for lib in libmudflap libgomp libssp libstdc++-v3/src; do
    make -j1 -C $CHOST/$lib DESTDIR=${pkgdir} install-toolexeclibLTLIBRARIES
  done  
  
  make -j1 -C $CHOST/libstdc++-v3/po DESTDIR=${pkgdir} install
  make -j1 -C $CHOST/libgomp DESTDIR=${pkgdir} install-info

  make -j1 DESTDIR=${pkgdir} install-target-libgfortran

  make -j1 DESTDIR=${pkgdir} install-target-libobjc

  rm -fr ${pkgdir}/lib/gcc
  
  #cleaning up the mess...
  find ${pkgdir} -name *.a -delete
  
  
  install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/COPYING.RUNTIME \
    ${pkgdir}/share/licenses/gcc-libs/RUNTIME.LIBRARY.EXCEPTION
}

package_gcc-fortran()
{
  pkgdesc="Fortran front-end for GCC"
  depends=("gcc=$pkgver-$pkgrel")
  install=gcc-fortran.install

  cd ${srcdir}/build
  make -j1 DESTDIR=$pkgdir install-target-libgfortran
  make -j1 -C $CHOST/libgomp DESTDIR=$pkgdir install-nodist_fincludeHEADERS
  make -j1 -C gcc DESTDIR=$pkgdir fortran.install-{common,man,info}
  install -Dm755 gcc/f951 $pkgdir/lib/gcc/$CHOST/$pkgver/f951
  
  # remove libraries included in gcc-libs
  rm -f ${pkgdir}/lib/libgfortran.so*
  
  # Install Runtime Library Exception
  install -Dm644 ${srcdir}/gcc-${pkgver}/COPYING.RUNTIME \
    ${pkgdir}/share/licenses/gcc-fortran/RUNTIME.LIBRARY.EXCEPTION
}

package_gcc-objc()
{
  pkgdesc="Objective-C front-end for GCC"
  depends=("gcc=$pkgver-$pkgrel")

  cd ${srcdir}/build
  make -j1 DESTDIR=$pkgdir install-target-libobjc
  install -dm755 $pkgdir/lib/gcc/$CHOST/$pkgver/
  install -m755 gcc/cc1obj{,plus} $pkgdir/lib/gcc/$CHOST/$pkgver/

  # remove libraries included in gcc-libs
  rm -f ${pkgdir}/lib/libobjc.so*

  # Install Runtime Library Exception
  install -Dm644 ${srcdir}/gcc-${pkgver}/COPYING.RUNTIME \
    ${pkgdir}/share/licenses/gcc-objc/RUNTIME.LIBRARY.EXCEPTION
}

