# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=libglade
pkgver=2.6.4
pkgrel=1
pkgdesc="Allows you to load glade interface files in a program at runtime"
arch=(i686)
license=('LGPL')
depends=('gtk2' 'libxml2')
makedepends=('python')
optdepends=('python: libglade-convert script')
options=('!libtool')
install=glade.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.6/${pkgname}-${pkgver}.tar.bz2
	libglade-2.0.1-nowarning.patch)
url="http://www.gnome.org"
md5sums=('d1776b40f4e166b5e9c107f1c8fe4139'
         '4f879c0ce550004905aa0fb24f25c353')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/libglade-2.0.1-nowarning.patch" || return 1
  ./configure --sysconfdir=/etc --prefix= --localstatedir=/var || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 libglade-convert "${pkgdir}/bin/" || return 1
}
