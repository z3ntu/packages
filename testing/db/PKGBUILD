# Maintainer: Michael Walker <mike@barrucadu.co.uk>
# Contributor: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=db
pkgver=5.1.25
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=('i686')
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
license=('custom')
groups=('base')
depends=('sh' 'gcc-libs')
options=('!libtool')
install=db.install
source=(http://download-uk.oracle.com/berkeley-db/db-${pkgver}.tar.gz)
md5sums=('06656429bfc1abb6c0498eaeff70cd04')

build() {
  cd ${srcdir}/$pkgname-${pkgver}/build_unix
  ../dist/configure --prefix= --enable-compat185 \
    --enable-shared --enable-static --enable-cxx --enable-dbm
  make LIBSO_LIBS=-lpthread
}

package(){
  cd ${srcdir}/$pkgname-${pkgver}/build_unix
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/docs
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE \
    ${pkgdir}/share/licenses/${pkgname}/LICENSE
}
