# $Id: PKGBUILD 84348 2010-06-29 18:03:55Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: damir <damir@archlinux.org>
# Contributor: Konstantinos Pachnis <kpachnis@mes-direct.com>
# Packaged by Stephen Gilles <stephendgilles@gmail.com>

pkgname=cmake
pkgver=2.8.2
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('i686')
license=('custom')
url="http://www.cmake.org"
depends=('expat' 'curl' 'gcc-libs' 'shared-mime-info')
install=${pkgname}.install
source=("http://www.cmake.org/files/v2.8/${pkgname}-${pkgver}.tar.gz"
	"hurd_complete_patch.patch")
md5sums=('8c967d5264657a798f22ee23976ff0d9'
         '991d95283400665c0c3a4735b5e5a460')

build() {
	cd $srcdir/$pkgname-$pkgver
	if [ -f .patched ]
	#my builds fail a lot...
	then
		echo "already patched..."
	else
		echo "patching..."
		patch -p1 < ${srcdir}/hurd_complete_patch.patch
		touch .patched
	fi
	./bootstrap --prefix= \
		--mandir=/share/man \
		--docdir=/share/doc/cmake \
		--system-libs
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install

	vimpath="$pkgdir/share/vim/vim72"
	install -Dm644 Docs/cmake-indent.vim $vimpath/indent/cmake-indent.vim
	install -Dm644 Docs/cmake-syntax.vim $vimpath/syntax/cmake-syntax.vim
	install -Dm644 Docs/cmake-mode.el $pkgdir/share/emacs/site-lisp/cmake-mode.el
	install -Dm644 Copyright.txt $pkgdir/share/licenses/$pkgname/LICENSE
}

