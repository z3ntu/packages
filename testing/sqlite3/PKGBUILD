# Maintainer: Michael Walker <mike@barrucadu.co.uk>
# Contributor: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgbase=sqlite3
pkgname=('sqlite3' 'sqlite3-doc')
_amalgamationver=3070400
_amalgamationver2=${_amalgamationver/00/}
pkgver=${_amalgamationver2//0/.}
pkgrel=1
arch=('i686')
license=('custom')
url="http://www.sqlite.org/"
makedepends=('tcl' 'readline>=6.0.00')
replaces=('sqlite')
source=(http://www.sqlite.org/sqlite-src-${_amalgamationver}.zip
        http://www.sqlite.org/sqlite-doc-${_amalgamationver}.zip
	license.txt)
md5sums=('2b5c53328505893599de6e2055996d7a'
         'c38a4aae9cf6e8921ba7ad48a9301288'
         'c1cdbc5544034d9012e421e75a5e4890')
options=(!libtool)

build() {
  cd ${srcdir}/sqlite-src-${_amalgamationver}
  export LTLINK_EXTRAS="-ldl"
  export CFLAGS="$CFLAGS -DSQLITE_ENABLE_FTS3=1 -DSQLITE_ENABLE_COLUMN_METADATA=1 -DSQLITE_ENABLE_UNLOCK_NOTIFY"
  ./configure --prefix= \
	--enable-threadsafe \
	--enable-threads-override-locks \
	--enable-cross-thread-connections \
	--disable-static \
	--enable-load-extension 

  # rpath removal
  sed -i 's|^hardcode_libdir_flag_spec=.*|hardcode_libdir_flag_spec=""|g' libtool
  sed -i 's|^runpath_var=LD_RUN_PATH|runpath_var=DIE_RPATH_DIE|g' libtool

  make 
}

package_sqlite3() {
  pkgdesc="A C library that implements an SQL database engine"
  depends=('readline>=6.0.00')

  cd ${srcdir}/sqlite-src-${_amalgamationver}
  make DESTDIR=${pkgdir} install 
  install -Dm0644 sqlite3.1 ${pkgdir}/share/man/man1/sqlite3.1

  # license
  install -Dm644 ${srcdir}/license.txt ${pkgdir}/share/licenses/${pkgname}/license.txt
}

package_sqlite3-doc() {
  pkgdesc="most of the static HTML files that comprise this website, including all of the SQL Syntax and the C/C++ interface specs and other miscellaneous documentation"

  cd ${srcdir}/sqlite-doc-${_amalgamationver}
  mkdir -p ${pkgdir}/share/doc/${pkgbase}
  cp -R *  ${pkgdir}/share/doc/${pkgbase}/
}