# Maintaner: Michael Walker <mike@barrucadu.co.uk>
# Contributor: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=glib2
pkgver=2.28.8
pkgrel=1
pkgdesc="Common C routines used by GTK+ 2.4 and other libs"
url="http://www.gtk.org/"
arch=('i686')
license=('LGPL')
depends=('pcre')
options=('!libtool' '!docs')
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/2.28/glib-${pkgver}.tar.xz
        glib2.sh
        glib2.csh)
sha256sums=('4d7ca95dbde8e8f60ab428c765b0dbb8a44be9eb9316491803ce5ee7b4748353'
            '9456872cdedcc639fb679448d74b85b0facf81033e27157d2861b991823b5a2a'
            '8d5626ffa361304ad3696493c0ef041d0ab10c857f6ef32116b3e2878ecf89e3')

build() {
  cd "${srcdir}/glib-${pkgver}"
  ./configure --prefix= --sysconfdir=/etc \
      --with-pcre=system --disable-fam 
  make
}

package() {
  cd "${srcdir}/glib-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/glib2.sh" "${pkgdir}/etc/profile.d/" 
  install -m755 "${srcdir}/glib2.csh" "${pkgdir}/etc/profile.d/" 

  for _i  in "${pkgdir}/etc/bash_completion.d/"*; do
    chmod +x "${_i}"
  done
}

check() {
  cd "${srcdir}/glib-${pkgver}"
  sed -i 's|!/bin/env python|!/bin/env python2|' gio/tests/gdbus-testserver.py
  make -k check || true
}