# Maintainer: Michael Walker <mike@barrucadu.co.uk>

pkgname=sudo
pkgver=1.7.4p4
pkgrel=1
pkgdesc="Give certain users the ability to run some commands as root"
arch=('i686')
url="http://www.sudo.ws/sudo/"
license=('custom')
depends=('glibc' 'pam')
backup=('etc/sudoers' 'etc/pam.d/sudo')
source=(ftp://ftp.sudo.ws/pub/sudo/$pkgname-$pkgver.tar.gz
        sudo.pam)
options=('!libtool' '!makeflags' 'force')
md5sums=('55d9906535d70a1de347cd3d3550ee87'
         '4e7ad4ec8f2fe6a40e12bcb2c0b256e3')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix= --with-pam --libexecdir=/lib \
    --with-env-editor --with-all-insults --with-logfac=auth

  sed -i 's/HAVE_LOCKF/HAVE_FLOCK/' config.h

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  install -dm755 $pkgdir/var/lib

  make DESTDIR=$pkgdir install

  install -Dm644 $srcdir/sudo.pam $pkgdir/etc/pam.d/sudo
  install -Dm644 LICENSE $pkgdir/share/licenses/sudo/LICENSE
}
