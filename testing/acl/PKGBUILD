# Maintainer: Michael Walker <mike@barrucadu.co.uk>
# Contributor: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=acl
pkgver=2.2.51
pkgrel=1
pkgdesc="Library for filesystem ACL support"
arch=('i686')
url="http://savannah.nongnu.org/projects/acl"
license=('LGPL')
groups=('base')
depends=('attr')
replaces=('xfsacl')
provides=('xfsacl')
conflicts=('xfsacl')
options=('!libtool')
source=(http://download.savannah.gnu.org/releases-noredirect/$pkgname/$pkgname-$pkgver.src.tar.gz acl-hurd-PATH_MAX.patch)
sha256sums=('06854521cf5d396801af7e54b9636680edf8064355e51c07657ec7442a185225'
            '1f3d5ad14dda6181589de4b2339520689956abf177a0765965529b6175c3cff0')

build() {
  cd $srcdir/acl-$pkgver
  patch -p1 < $srcdir/acl-hurd-PATH_MAX.patch

  export INSTALL_USER=root INSTALL_GROUP=root
  ./configure --prefix= --libdir=/lib --libexecdir=/lib  
  make
}

package() {
  cd $srcdir/acl-$pkgver

  make DIST_ROOT="${pkgdir}" install install-dev install-lib

  rm -f ${pkgdir}/lib/libacl.a
  chmod 0755 ${pkgdir}/lib/libacl.so.*.*.*
}
