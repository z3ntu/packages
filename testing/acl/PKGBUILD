# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=acl
pkgver=2.2.49
pkgrel=3
pkgdesc="Library for filesystem ACL support"
arch=('i686')
url="http://savannah.nongnu.org/projects/acl"
license=('LGPL')
depends=('attr')
replaces=('xfsacl')
provides=('xfsacl')
conflicts=('xfsacl')
options=('!libtool')
source=(http://mirrors.zerg.biz/nongnu/${pkgname}/${pkgname}-${pkgver}.src.tar.gz
	acl-hurd-PATH_MAX.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < $srcdir/acl-hurd-PATH_MAX.patch

  export INSTALL_USER=root INSTALL_GROUP=root
  ./configure --prefix= --libdir=/lib --libexecdir=/lib 
  make  
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DIST_ROOT="${pkgdir}" install install-dev install-lib

  rm ${pkgdir}/lib/libacl.a
  chmod 0755 ${pkgdir}/lib/libacl.so.*.*.*
}
md5sums=('181445894cca986da9ae0099d5ce2d08'
         '10e571a4c8a9d0e0a8e4e01c77733da7')
