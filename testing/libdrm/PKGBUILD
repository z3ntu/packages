# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=libdrm
pkgver=2.4.21
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('i686')
license=('custom')
depends=('glibc')
options=('!libtool')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        no-pthread-stubs.patch
        COPYING)
     
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/no-pthread-stubs.patch"
  libtoolize --force 
  aclocal 
  autoconf 
  automake 
 ./configure --prefix= \
      --disable-intel --disable-radeon \
      --disable-vmwgfx-experimental-api \
			--disable-nouveau-experimental-api 

  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install 
  install -m755 -d "${pkgdir}/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/share/licenses/${pkgname}/" 
}
md5sums=('273ed9dad986e3a931649f3d8762ff74'
         '9b5ec3bbebe86921e2057694c42f65b8'
         'ba65e71c481b94ef0fb6c23c7f21ffa1')
