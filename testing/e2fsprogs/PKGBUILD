# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com> 

pkgname=e2fsprogs
pkgver=1.41.14
pkgrel=1
pkgdesc="Ext2/3/4 filesystem utilities"
arch=('i686')
license=('GPL' 'LGPL' 'MIT')
url="http://e2fsprogs.sourceforge.net"
groups=('base')
depends=('sh' 'util-linux-ng')
makedepends=('bc')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
	'e2fsprogs-O_DIRECT.patch'
        'MIT-LICENSE')
backup=('etc/mke2fs.conf')
install=${pkgname}.install
options=(!zipman)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Remove unnecessary init.d directory
  sed -i '/init\.d/s|^|#|' misc/Makefile.in 

  patch -p1 -i "${srcdir}/e2fsprogs-O_DIRECT.patch"

  ./configure --prefix= --with-root-prefix="" --enable-elf-shlibs \
      --disable-fsck --disable-uuidd \
      --disable-libuuid --disable-libblkid 

  make 
}

package() { 
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install install-libs 
  
  sed -i -e 's/^AWK=.*/AWK=awk/' "${pkgdir}/bin/compile_et" 

  # install MIT license
  install -Dm644 "${srcdir}/MIT-LICENSE" \
    "${pkgdir}/share/licenses/${pkgname}/MIT-LICENSE"
}
md5sums=('05f70470aea2ef7efbb0845b2b116720'
         '035b7c69b7a2cecf996a4708c262245e')
md5sums=('05f70470aea2ef7efbb0845b2b116720'
         '692792e9c541fc826029aeea7e50657c'
         '035b7c69b7a2cecf996a4708c262245e')
