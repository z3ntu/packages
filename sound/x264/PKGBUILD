# Maintainer: Michael Walker <mike@barrucadu.co.uk>

pkgname=x264
pkgver=20100410
pkgrel=1
pkgdesc="free library for encoding H264/AVC video streams"
arch=('i686')
url="http://www.videolan.org/developers/x264.html"
license=('GPL')
depends=('glibc')
makedepends=('yasm')
source=(ftp://ftp.videolan.org/pub/videolan/x264/snapshots/x264-snapshot-$pkgver-2245.tar.bz2)
sha256sums=('7f42481ad03ffdf04e182fcc336f86e3eec6cdaffb5385ca4e713541b8705185')

build() {
    cd "$srcdir/$pkgname-snapshot-$pkgver-2245" || return 1

    sed -i 's/*linux*/*gnu*/' configure
    sed -i 's/SYS="LINUX"/SYS="HURD"/' configure

    ./configure --enable-shared --host=i686-pc-gnu || return 1
    make || return 1
}

package() {
    cd "$srcdir/$pkgname-snapshot-$pkgver-2245" || return 1

    make DESTDIR="$pkgdir" \
      bindir=/bin \
      libdir=/lib \
      includedir=/include \
      install || return 1
}

# vim:set ts=2 sw=2 et:
