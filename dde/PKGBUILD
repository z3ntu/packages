# Maintainer: Stephen Gilles <stephendgilles@gmail.com>
# Contributor: Diegonc <dnietoc@gmail.com>

pkgname=(
  'ddeutils'
  'libbpf'
  'dde-driver-ne2k_pci'
  'dde-driver-pcnet32')
pkgbase=dde
pkgver=20100903
pkgrel=1
pkgdesc="DDE split package"
arch=(i686)
url="http://www.gnu.org/software/hurd/hurd.html"
license=('GPL')
depends=(gnumach-userleveldrivers libpcap)
#depends=(libpcap)

_gitroot="git://git.sv.gnu.org/hurd/incubator.git"
_gitname=dde

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d "$_gitname" ]; then
    cd "$_gitname"
    git pull origin || return 0
    # current build process is two-step, and the second-step cannot have 
    # internet access
    msg "The local files are updated."
  else
    git clone -b "$_gitname" "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #
 
  export LDFLAGS=""
  autoreconf -vif
  ./configure --prefix= --disable-profile

  find -name "*.d" | xargs rm

  make libmachdev
  make libhurd-slab
  make -C libbpf
  make pfinet
  make devnode
  make libddekit
  make -C libdde_linux26
  LIBS="-L../libbpf" make -C dde_pcnet32
  LIBS="-L../libbpf" make -C dde_ne2k_pci
}

package_ddeutils() {
  pkgdesc="DDE support utilities."
  arch=(i686)
  license=('GPL')
  depends=(gnumach-userleveldrivers libpcap)

  cd "$srcdir/$_gitname-build"
  make prefix="$pkgdir" -C pfinet install
  make prefix="$pkgdir" -C devnode install
  install -m755 -d "$pkgdir/hurd/dde"
  mv "$pkgdir"/hurd/{pfinet,devnode} "$pkgdir/hurd/dde/"
}

package_libbpf() {
  pkgdesc="A packet filtering library."
  arch=(i686)
  license=('GPL')
  depends=(gnumach-userleveldrivers)

  cd "$srcdir/$_gitname-build/libbpf"
  make prefix="$pkgdir" install
}

package_dde-driver-ne2k_pci() {
  pkgdesc="ne2k_pci DDE driver."
  arch=(i686)
  license=('GPL')
  depends=(gnumach-userleveldrivers libbpf)

  cd "$srcdir/$_gitname-build/dde_ne2k_pci"
  install -D -m755 dde_ne2k_pci "$pkgdir/hurd/dde/dde_ne2k_pci"
}

package_dde-driver-pcnet32() {
  pkgdesc="pcnet32 DDE driver."
  arch=(i686)
  license=('GPL')
  depends=(gnumach-userleveldrivers libbpf)

  cd "$srcdir/$_gitname-build/dde_pcnet32"
  install -D -m755 dde_pcnet32 "$pkgdir/hurd/dde/dde_pcnet32"
}

# vim: set sw=2 ts=2 et:
