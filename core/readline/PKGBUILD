# Maintainer: Stephen Gilles <stephendgilles@gmail.com>
# Contributor: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=readline
_patchlevel=001 #prepare for some patches
pkgver=6.2.$_patchlevel
pkgrel=1
pkgdesc="GNU readline library"
arch=('i686')
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
depends=('glibc' 'ncurses')
backup=("etc/inputrc")
source=(http://ftp.gnu.org/gnu/readline/readline-6.2.tar.gz
        inputrc)
if [ $_patchlevel -gt 00 ]; then
    for p in $(seq -w 01 $_patchlevel); do
        source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-6.2-patches/readline62-$p)
    done
fi

build() {
  cd ${srcdir}/${pkgname}-6.2
  for p in ../readline61-*; do
    [ -e "$p" ] || continue
    msg "applying patch ${p}"
    patch -Np0 -i ${p}
  done

  # Remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf

  ./configure --prefix= --libdir=/lib \
        --mandir=/share/man --infodir=/share/info
  make SHLIB_LIBS=-lncursesw
}

package() {
  cd ${srcdir}/${pkgname}-6.2
  make DESTDIR=${pkgdir} install
  
  mkdir -p ${pkgdir}/etc
  install -m644 ../inputrc ${pkgdir}/etc/inputrc
}
md5sums=('67948acb2ca081f23359d0256e9a271c'
         'e5fc955f56d9fa5beb871f3279b8fa8b'
         '83287d52a482f790dfb30ec0a8746669')
