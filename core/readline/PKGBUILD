# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=readline
_patchlevel=002 #prepare for some patches
pkgver=6.1.$_patchlevel
pkgrel=1
pkgdesc="GNU readline library"
arch=('i686')
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
depends=('glibc' 'ncurses')
backup=("etc/inputrc")
source=(http://ftp.gnu.org/gnu/readline/readline-6.1.tar.gz
        inputrc)
if [ $_patchlevel -gt 00 ]; then
    for p in $(seq -w 01 $_patchlevel); do
        source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-6.1-patches/readline61-$p)
    done
fi
md5sums=('fc2f7e714fe792db1ce6ddc4c9fb4ef3'
         'e5fc955f56d9fa5beb871f3279b8fa8b'
         'c642f2e84d820884b0bf9fd176bc6c3f'
         '1a76781a1ea734e831588285db7ec9b1')

build() {
  cd ${srcdir}/${pkgname}-6.1
  for p in ../readline61-*; do
    [ -e "$p" ] || continue
    msg "applying patch ${p}"
    patch -Np0 -i ${p} || return 1
  done

  # Remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf

  ./configure --prefix= --libdir=/lib \
        --mandir=/share/man --infodir=/share/info
  make SHLIB_LIBS=-lncursesw || return 1
}

package() {
  cd ${srcdir}/${pkgname}-6.1
  make DESTDIR=${pkgdir} install || return 1
  
  mkdir -p ${pkgdir}/etc
  install -m644 ../inputrc ${pkgdir}/etc/inputrc || return 1
}
