# Maintainer: Matthias Lanzinger <mlaenz@gmail.com>

pkgname=pacman
pkgver=3.5.2
pkgrel=2
pkgdesc="A library-based package manager with dependency support"
arch=(i686)
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('bash' 'libarchive>=2.8.4-2' 'libfetch>=2.28' 'pacman-mirrorlist')
optdepends=('fakeroot: for makepkg usage as normal user'
	    'curl: for rankmirrors usage')
backup=(etc/pacman.conf etc/makepkg.conf)
source=(ftp://ftp.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz
        pacman.conf
        makepkg.conf
        pacman_hurd.patch
        pacman_hurd_fakeroot.patch)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np2 < $srcdir/pacman_hurd.patch

  #as-needed causes a segfault
  export LDFLAGS=""
  ./configure --prefix= --sysconfdir=/etc --localstatedir=/var --enable-doc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -m644 ${srcdir}/pacman.conf ${pkgdir}/etc/pacman.conf

  sed -i ${pkgdir}/etc/makepkg.conf \
    -e "s|@CARCH[@]|1686|g" \
    -e "s|@CHOST[@]|i686-pc-gnu|g" \
    -e "s|@CARCHFLAGS[@]|-march=i686 |g"



  # install completion files
  mkdir -p $pkgdir/etc/bash_completion.d/
  install -m644 contrib/bash_completion $pkgdir/etc/bash_completion.d/pacman
  mkdir -p $pkgdir/share/zsh/site-functions/
  install -m644 contrib/zsh_completion $pkgdir/share/zsh/site-functions/_pacman

  #patch for hurd fakeroot
  cd $pkgdir
  patch -Np1 < $srcdir/pacman_hurd_fakeroot.patch || true
}

md5sums=('b4f1fdbc17100923071ebe8fe9377be5'
         'e330888b8ad2307d87d69eb187521127'
         '6b2bdf5a9bf2635c1e6f951bd5933bd0'
         '210e7165adf4fa8822c4eb935bc92935'
         '69f6dc180ea25c38a6376a6d5fd1303d')
