# Maintainer: Matthias Lanzinger <mlaenz@gmail.com>

pkgname=pacman
pkgver=3.4.1
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=(i686)
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('bash' 'libarchive>=2.8.4-2' 'libfetch' 'pacman-mirrorlist')
optdepends=('fakeroot: for makepkg usage as normal user')
backup=(etc/pacman.conf etc/makepkg.conf)
source=(ftp://ftp.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz
        pacman.conf
        makepkg.conf
        pacman_hurd.patch
        pacman_hurd_fakeroot.patch)
md5sums=('4e4e8e4f636d9878fbed9cf840f162cf'
         '7e6f21f9990f986edfcb9e573ff4f9f2'
         '6cdd59f2a598861f084748d567d49599'
         '210e7165adf4fa8822c4eb935bc92935'
         'd248452ac7df02e6988f49055aaae4ed')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np2 < $srcdir/pacman_hurd.patch

  #as-needed causes a segfault
  export LDFLAGS=""
  ./configure --prefix= --sysconfdir=/etc --localstatedir=/var --enable-doc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  mkdir -p $pkgdir/etc/pacman.d
  install -m644 $srcdir/pacman.conf $pkgdir/etc
  install -m644 $srcdir/makepkg.conf $pkgdir/etc

  # install completion files
  mkdir -p $pkgdir/etc/bash_completion.d/
  install -m644 contrib/bash_completion $pkgdir/etc/bash_completion.d/pacman
  mkdir -p $pkgdir/share/zsh/site-functions/
  install -m644 contrib/zsh_completion $pkgdir/share/zsh/site-functions/_pacman

  #patch for hurd fakeroot
  cd $pkgdir
  patch -Np1 < $srcdir/pacman_hurd_fakeroot.patch
}


