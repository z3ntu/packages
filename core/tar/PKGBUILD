# Maintainer: Matthias Lanzinger <mlaenz@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=tar
pkgver=1.25
pkgrel=1
pkgdesc="Utility used to store, backup, and transport files"
arch=('i686')
url="http://www.gnu.org/software/tar/tar.html"
license=('GPL3')
groups=('base')
depends=('glibc' 'sh')
options=('!emptydirs')
install=tar.install
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.bz2
        tar.1)
md5sums=('6e497f861c77bbba2f7da4e10270995b'
         'e0538778516a284e3558c454b2576c2f')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix= --libexecdir=/lib/tar --bindir=/bin
  make
  make check
}
  
package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install
  install -Dm644 ${srcdir}/tar.1 ${pkgdir}/share/man/man1/tar.1
}
