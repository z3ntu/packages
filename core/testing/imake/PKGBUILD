# Maintainer: Matt Windsor <hayashi@archhurd.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=imake
pkgver=1.0.3
pkgrel=2
pkgdesc="X.Org imake program and related utilities"
arch=(i686)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=(glibc)
makedepends=(pkgconfig xproto perl)
optdepends=('perl: for mkhtmlindex')
source=(http://xorg.freedesktop.org/releases/individual/util/imake-1.0.3.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/gccmakedep-1.0.2.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/lndir-1.0.2.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/makedepend-1.0.2.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/xorg-cf-files-1.0.3.tar.bz2
	hurdconfig.patch
	xorg-cf-files-1.0.2-xprint.patch
	LICENSE)
md5sums=('ff553c4646edcc9e76b7308991ad421a'
         'b533c0771dbbaf9b041ff35bb941d3a2'
         '619acbb2ed766d7762f02328513b1f72'
         '62e58330fe8d1e3e28c7a45779833a48'
         '10546b5ddda3cdda7950bb56bf98e0ea'
         'b0582a9ba144080ecb8571b621d4f147'
         '82356cfc8a761c85b0b37736fc91f604'
         'f5767133ef7a98a0bfa15139e0e3d32e')

build() {
  cd ${srcdir}/xorg-cf-files-1.0.3*
  patch -Np1 -i "${srcdir}/hurdconfig.patch"
  patch -Np1 -i "${srcdir}/xorg-cf-files-1.0.2-xprint.patch"
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix= --mandir=/share/man
      make
      popd
    fi
  done
}

package() {
	cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install
      popd
    fi
  done
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/share/licenses/${pkgname}/LICENSE"
}
