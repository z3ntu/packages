# Maintainer Matthias Lanzinger <mlaenz@gmail.com>

pkgname=bash
_patchlevel=005
pkgver=4.1.$_patchlevel
pkgrel=3
pkgdesc="The GNU Bourne Again shell"
arch=(i686)
url="http://www.gnu.org/software/bash/bash.html"
license=('GPL')
groups=('base')
depends=('glibc')
provides=('sh')
install=bash.install
source=(ftp://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-4.1.tar.gz)
if [ $_patchlevel -gt 000 ]; then
    for p in $(seq -w 001 $_patchlevel); do
        source=(${source[@]} http://ftp.gnu.org/gnu/bash/bash-4.1-patches/bash41-$p)
    done
fi
md5sums=('9800d8724815fd84994d9be65ab5e7b8'
         '582dea5671b557f783e18629c2f77b68'
         '118d465095d4a4706eb1d34696a2666a'
         '120f7cf039a40d35fe375e59d6f17adc'
         '336ee037fc2cc1e2350b05097fbdc87c'
         '9471e666797f0b03eb2175ed752a9550')


build() {
  cd "$srcdir/$pkgname-4.1"
  for p in ../bash41-*; do
    [ -e "$p" ] || continue
    msg "applying patch ${p}"
    patch -Np0 -i ${p} || return 1
  done

  ./configure --prefix=
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-4.1"
  make DESTDIR="$pkgdir/" install
  cd $pkgdir/bin
  ln -s bash sh
}

# vim:set ts=2 sw=2 et:
