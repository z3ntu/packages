# Maintainer Matthias Lanzinger <mlaenz@gmail.com>

pkgname=bash
_patchlevel=009
pkgver=4.1.$_patchlevel
pkgrel=1
pkgdesc="The GNU Bourne Again shell"
arch=(i686)
url="http://www.gnu.org/software/bash/bash.html"
license=('GPL')
groups=('base')
backup=(etc/profile.bash etc/skel/.bashrc etc/skel/.bash_profile)
depends=('glibc' 'readline')
provides=('sh')
install=bash.install
source=(ftp://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-4.1.tar.gz
        profile.bash
        bashrc
        enable-system-config-files.patch
        system.bashrc)
if [ $_patchlevel -gt 000 ]; then
    for p in $(seq -w 001 $_patchlevel); do
        source=(${source[@]} http://ftp.gnu.org/gnu/bash/bash-4.1-patches/bash41-$p)
    done
fi
md5sums=('9800d8724815fd84994d9be65ab5e7b8'
         '17f20ec69535a2f50f2112a8c700630d'
         '3d2837e84645eac9bde399b58014bafa'
         '51725defa4c2dec49c1bc15883e0bee2'
         '196697769f1667a8a1aed608811129c6'
         '582dea5671b557f783e18629c2f77b68'
         '118d465095d4a4706eb1d34696a2666a'
         '120f7cf039a40d35fe375e59d6f17adc'
         '336ee037fc2cc1e2350b05097fbdc87c'
         '9471e666797f0b03eb2175ed752a9550'
         'fb80ccd58cb1e34940f3adf4ce6e4a1e'
         '192a8b161d419a1d0d211169f1d1046e'
         '1dc5a0253827decbe6aa529cde83e154'
         'ea361b842b7405a7f5ebbabbdf74372f')




build() {
  cd "$srcdir/$pkgname-4.1"
  for p in ../bash41-*; do
    [ -e "$p" ] || continue
    msg "applying patch ${p}"
    patch -Np0 -i ${p} || return 1
  done


  patch -Np0 -i $srcdir/enable-system-config-files.patch || return 1


  ./configure --prefix= --with-curses --enable-readline \
    --without-bash-malloc --with-installed-readline \
    --mandir=/share/man --infodir=/share/info
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-4.1"
  make DESTDIR="$pkgdir/" install

  

  install -D -m644 ${srcdir}/profile.bash ${pkgdir}/etc/profile.bash || return 1
  install -D -m644 ${srcdir}/system.bashrc ${pkgdir}/etc/bash.bashrc || return 1

  mkdir -p ${pkgdir}/etc/skel/
  install -D -m644 ${srcdir}/bashrc ${pkgdir}/etc/skel/.bashrc
  echo ". \$HOME/.bashrc" >${pkgdir}/etc/skel/.bash_profile

  # make bash our default sh
  cd $pkgdir/bin
  ln -s bash sh
}

# vim:set ts=2 sw=2 et:
