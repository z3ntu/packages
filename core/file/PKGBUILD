# Maintainer: Stephen Gilles <stephendgilles@gmail.com>
# Contributor: Alexander Preisinger <alexander.preisinger@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=file
pkgver=5.07
pkgrel=1
pkgdesc="File type identification utility"
arch=('i686')
license=('custom')
groups=('base')
url="http://www.darwinsys.com/file/"
depends=('glibc' 'zlib')
options=('!libtool')
source=(ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz
        file-5.05-MAXPATHLEN.patch
	file-5.07-zip-detect.patch)

build() {
  cd ${srcdir}/$pkgname-$pkgver

  if [ -e ${srcdir}/.patched ]
  then
    msg "Already patched"
  else
    msg "Patching"
    patch src/magic.c < ${srcdir}/file-5.05-MAXPATHLEN.patch
    patch -p0  < ${srcdir}/file-5.07-zip-detect.patch
    touch ${srcdir}/.patched
  fi

  ./configure --prefix= --datadir=/share/file
  make
}

package(){
  cd ${srcdir}/$pkgname-$pkgver
  
  make DESTDIR=${pkgdir} install
  install -dm755 ${pkgdir}/share/misc
  ln -s ../file/magic.mgc ${pkgdir}/share/misc
  
  install -D -m644 COPYING ${pkgdir}/share/licenses/${pkgname}/COPYING
}
md5sums=('b8d1f9a8a644067bd0a703cebf3f4858'
         'd2d5ec39b67020188e894dc8201146a2'
         'bcbf2e152f38003a2736298bbd0f37f8')
