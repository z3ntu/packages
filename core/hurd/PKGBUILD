# Maintainer Matthias Lanzinger <mlaenz@gmail.com>

pkgname=hurd
pkgver=20100321
pkgrel=2
pkgdesc="The GNU Hurd"
arch=(i686)
url="http://www.gnu.org/software/hurd/hurd.html"
license=('GPL')
groups=('base')
depends=('gnumach' 'glibc' )
makedepends=('git' 'glibc')
options=('!makeflags')
source=(tls_support.hurd.patch
        tls_support.libpthread.patch
        libpthread_fix.patch
        pflocal.patch
        rc.patch
        ttys)
md5sums=('af87e4b04d89392a2b6e7751361f2460'
         '15886845ff60d66e767b8eb88c501458'
         'f745ddc28dfe41fa42087fbb7dbd6b65'
         '44291124d0b08ed4a735c42f06ceec28'
         'f404f1b3fbfb6f57aff4b470c7fadc07'
         'f2ae144d872ec41cc4f2e070df71dfb1')


_hurdgitroot="git://git.sv.gnu.org/hurd/hurd.git"
_pthreadgitroot="git://git.sv.gnu.org/hurd/libpthread.git"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d hurd -a -d libpthread ] ; then
    cd hurd && git pull origin && cd ..
    cd libpthread && git pull origin && cd ..
    msg "The local files are updated."
  else
    #In case only one dir exists (lazy solution)
    rm -rf hurd libpthread
    
    git clone $_hurdgitroot hurd
    git clone $_pthreadgitroot libpthread
  fi
  msg "GIT checkout done or server timeout"
  
  rm -rf "$srcdir/build"
  cp -r "$srcdir/hurd" "$srcdir/build"
  cd "$srcdir/build"
  patch -Np1 < $srcdir/tls_support.hurd.patch
  patch -Np0 < $srcdir/pflocal.patch

  cp -a ../libpthread .
  cd libpthread
  patch -Np1 < $srcdir/libpthread_fix.patch
  patch -Np1 < $srcdir/tls_support.libpthread.patch
  cd ..

  #
  # BUILD HERE
  #
  autoreconf -vif ../hurd
  ./configure --host=i686-pc-gnu --prefix= --disable-profile
  make || return 1
}

package() {
  cd "$srcdir/build"
  make prefix="$pkgdir/" install || return 1
  make prefix="$pkgdir/" libpthread-install || return 1

  # install msgids
  make -C hurd/ hurd.msgids || return 1
  mkdir $pkgdir/share/msgids
  cp hurd/*.msgids $pkgdir/share/msgids

  install -m 644 $srcdir/ttys $pkgdir/etc/ttys

  # Remove daemon starting from /libexec/rc. 
  # We want our initscripts to do that.
  cd $pkgdir
  patch -Np1 < $srcdir/rc.patch
} 

