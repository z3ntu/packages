# Maintainer Stephen Gilles <stephendgilles@gmail.com>
# Maintainer Matthias Lanzinger <mlaenz@gmail.com>

pkgname=hurd
pkgver=$(date +%Y%m%d)
pkgrel=1
pkgdesc="The GNU Hurd"
arch=(i686)
url="http://www.gnu.org/software/hurd/hurd.html"
license=('GPL')
groups=('base')
depends=('gnumach' 'glibc' 'initscripts')
makedepends=('git' 'glibc')
options=('!makeflags')
backup=(etc/ttys)
source=(01_libports-prevent-hang.patch
        ttys)

_hurdgitroot="git://git.sv.gnu.org/hurd/hurd.git"
_pthreadgitroot="git://git.sv.gnu.org/hurd/libpthread.git"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d hurd -a -d libpthread ] ; then
    cd hurd && git pull &&  cd ..
    cd libpthread && git pull origin &&  cd ..
    msg "The local files are updated."
  else
    #In case only one dir exists (lazy solution)
    rm -rf hurd libpthread

    git clone $_hurdgitroot hurd
    git clone $_pthreadgitroot libpthread

  fi
  msg "GIT checkout done or server timeout"

  if [ -e ${srcdir}/.prepared ]; then
    msg2 "Build directory already prepared"
  else
    msg2 "Preparing build directory"
    rm -rf "$srcdir/build"
    cp -r "$srcdir/hurd" "$srcdir/build"
    cd "$srcdir/build"

    # Diego Nieto Cid's patch to prevent hanging
    patch -Np1 -i ${srcdir}/01_libports-prevent-hang.patch

    cp -a ../libpthread .
    touch ${srcdir}/.prepared
  fi

  #
  # BUILD HERE
  # 
  export LDFLAGS="$(echo $LDFLAGS | sed 's/-Wl,--as-needed//')"
  
  if [ -e ${srcdir}/.configured ]; then
    msg2 "Already configured"
  else
    msg2 "Configuring"
    autoreconf -vif 
    ./configure --prefix= --disable-profile \
              --enable-static-progs='iso9660fs,ext2fs,ufs' \
              --without-parted
    touch ${srcdir}/.configured
  fi

  # Fakeroot or --asroot may break this.  May need to be run by hand
  make libihash || true
  make -C libpthread || true
  make
}

package() {
  cd "$srcdir/build"
  make prefix="$pkgdir/" install
  make prefix="$pkgdir/" libpthread-install

  # install msgids
  make -C hurd/ hurd.msgids
  mkdir $pkgdir/share/msgids
  cp hurd/*.msgids $pkgdir/share/msgids

  install -m 644 $srcdir/ttys $pkgdir/etc/ttys

  # system startup scripts are in the initscripts package
  rm $pkgdir/libexec/runsystem $pkgdir/libexec/rc

  # for rc.shutdown
  mv $pkgdir/sbin/halt $pkgdir/sbin/internal_halt
  mv $pkgdir/sbin/reboot $pkgdir/sbin/internal_reboot

  #for pthread weirdness
  mkdir -p ${pkgdir}/include/gnu
  cp ${srcdir}/stubs-pthread.h ${pkgdir}/include/gnu/
}

