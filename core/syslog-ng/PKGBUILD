# Maintainer: Stephen Gilles <stephendgilles@gmail.com>
# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=syslog-ng
pkgver=3.3.1
pkgrel=1
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities"
arch=('i686')
license=('GPL2')
groups=('base')
url="http://www.balabit.com/network-security/syslog-ng/"
depends=('glib2' 'eventlog' 'openssl' 'awk')
makedepends=('flex' 'pkg-config')
optdepends=('logrotate')
provides=('logger')
option=('!libtool')
backup=('etc/syslog-ng/modules' 'etc/logrotate.d/syslog-ng'
				'etc/syslog-ng/scl.conf' 'etc/syslog-ng/syslog-ng.conf')
source=(http://www.balabit.com/downloads/files/syslog-ng/sources/$pkgver/source/$pkgname\_$pkgver.tar.gz
        syslog-ng.conf
        syslog-ng.logrotate
        syslog-ng.rc
	01-IOV_MAX.patch
	02-hurd-support.patch)
install=syslog-ng.install

build() {
  cd $srcdir/$pkgname-$pkgver

  if [ -e ${srcdir}/.patched ]; then
    msg2 "Already patched"
  else
    msg2 "Patching"
    patch -Np1 -i ${srcdir}/01-IOV_MAX.patch
    patch -Np1 -i ${srcdir}/02-hurd-support.patch
    touch ${srcdir}/.patched
  fi

  if [ -e ${srcdir}/.configured ]; then
    msg2 "Already configured"
  else
    msg2 "Configuring"
    ./configure \
      --prefix= --sysconfdir=/etc/syslog-ng \
      --libexecdir=/lib \
      --localstatedir=/var/lib/syslog-ng \
      --disable-tcp-wrapper \
      --with-pidfile-dir=/var/run \
      --disable-spoof-source \
      --disable-linux-caps
    touch ${srcdir}/.configured
  fi

  make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install 
    install -dm755 ${pkgdir}/var/lib/syslog-ng ${pkgdir}/etc/ysslog-ng/patterndb.d
    install -Dm644 ${srcdir}/syslog-ng.conf ${pkgdir}/etc/syslog-ng.conf
    install -Dm644 ${srcdir}/syslog-ng.logrotate ${pkgdir}/etc/logrotate.d/syslog-ng
    install -Dm755 ${srcdir}/syslog-ng.rc ${pkgdir}/etc/rc.d/syslog-ng
    # remove libexec
    rm -rf ${pkgdir}/libexec

    # add missing symlink
    cd ${pkgdir}/lib
    ln -s libsyslog-ng.so{,.0}

    # fix conflict with every other packate
    if [ -e ${pkgdir}/share/info/dir ]; then
      rm ${pkgdir}/share/info/dir
    fi
}

md5sums=('a3dfe8a49aa8ce1aeb7e1ed5b11af378'
         'd02e403f7877ba51511c36114675811d'
         '735636090be4582885974542d2a75855'
         'ee7783bf72bbde82456e37117fe7426d'
         '3506e1c6c05fa894c2d97a2c8f14dfb0'
         '95aa944947a24b162c37b4b38c3931ce')
