# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>
# $Id: PKGBUILD 83737 2010-06-22 18:32:52Z andrea $
# Maintainer:
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=libldap
pkgver=2.4.22
pkgrel=2
pkgdesc="Lightweight Directory Access Protocol (LDAP) client libraries"
arch=('i686')
license=('custom')
url="http://www.openldap.org/"
backup=(etc/openldap/ldap.conf)
depends=('libsasl' 'openssl')
makedepends=('tcp_wrappers')
options=('!libtool')
source=("ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/openldap-${pkgver}.tgz"
        'hurd_sasl.patch'
        'ntlm.patch')

build() {
  cd ${srcdir}/openldap-${pkgver}
	export LDFLAGS=""
  patch -Np1 -i ${srcdir}/ntlm.patch
  patch -p1 -i ${srcdir}/hurd_sasl.patch

  ./configure --prefix= \
              --libexecdir=/sbin \
              --sysconfdir=/etc \
	      --mandir=/share/man \
              --localstatedir=/var/lib/openldap \
              --enable-crypt --enable-dynamic \
              --with-threads --enable-wrappers \
	      --enable-spasswd --with-cyrus-sasl \
	      --disable-bdb --disable-hdb

  cd include
  make

  cd ../libraries
  make depend
  make

  cd ../doc/man/man3
  make
  
  cd ../man5
  make
}

package() {
  cd ${srcdir}/openldap-${pkgver}

  cd include
  make DESTDIR=${pkgdir} install

  cd ../libraries
  make DESTDIR=${pkgdir} install

  cd ../doc/man/man3
  make DESTDIR=${pkgdir} install

  cd ../man5
  install -Dm644 ldap.conf.5.tmp \
    ${pkgdir}/share/man/man5/ldap.conf.5
  
  # get rid of duplicate default conf files
  rm ${pkgdir}/etc/openldap/*.default

  ln -sf liblber.so ${pkgdir}/lib/liblber.so.2
  ln -sf libldap.so ${pkgdir}/lib/libldap.so.2

  install -Dm644 ${srcdir}/openldap-${pkgver}/LICENSE \
    ${pkgdir}/share/licenses/$pkgname/LICENSE
}
md5sums=('ef01b52255ce8e3fbf8aa34f6fe7598b'
         'c53d3439f8bfb336f1531ce9442e27aa'
         'dfa9eb6f2fd9d0a3dab0e1860923489e')
