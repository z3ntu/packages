# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgname=inetutils
pkgver=1.7
pkgrel=1
pkgdesc="A collection of common network programs"
arch=('i686')
url="http://www.gnu.org/software/inetutils/"
license=('GPL')
replaces=('hostname')
depends=('readline' 'ncurses' 'pam' 'tcp_wrappers')
backup=('etc/conf.d/ftpd' 'etc/xinetd.d/telnet' 'etc/xinetd.d/talk')
options=('!emptydirs')
install=inetutils.install
source=(http://ftp.ginu.org/gnu/inetutils/${pkgname}-${pkgver}.tar.gz \
        ftpd.rc ftpd.conf telnet.xinetd talk.xinetd)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix= --libexec=/sbin --localstatedir=/var \
    --mandir=/share/man --infodir=/share/info \
    --with-wrap --with-pam \
    --enable-ftp --enable-ftpd \
    --enable-telnet --enable-telnetd \
    --enable-talk --enable-talkd \
    --disable-rlogin --disable-rlogind \
    --disable-rsh --disable-rshd \
    --disable-rexec --disable-rexecd \
    --disable-rcp \
    --disable-tftp --disable-tftpd \
    --enable-ping --enable-ping6 \
    --disable-logger --enable-syslogd \
    --enable-inetd --enable-whois \
    --disable-uucpd --enable-hostname \
    --enable-ifconfig --disable-traceroute || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1

  install -D -m755 "${srcdir}/ftpd.rc" "${pkgdir}/etc/rc.d/ftpd" || return 1
  install -D -m644 "${srcdir}/ftpd.conf" "${pkgdir}/etc/conf.d/ftpd" || return 1
  install -D -m644 "${srcdir}/telnet.xinetd" "${pkgdir}/etc/xinetd.d/telnet" || return 1
  install -D -m644 "${srcdir}/talk.xinetd" "${pkgdir}/etc/xinetd.d/talk" || return 1
}
md5sums=('a1d5a01b0ab8a7e596ac4cff0cce7129'
         'abb31b95485802d23b7df3f96fa3c75a'
         'e6e6e5990a2e8159a276fef8fbf54c04'
         '433b6535e931f22654102de62402d76e'
         'c84c281926ec95095971a666b3b11516')
