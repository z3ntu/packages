# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>
# $Id: PKGBUILD 78109 2010-04-19 11:02:50Z allan $
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libsasl
pkgver=2.1.23
pkgrel=2
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library"
arch=('i686')
url="http://cyrusimap.web.cmu.edu/downloads.html#sasl"
license=('custom')
depends=('db')
optdepends=('cyrus-sasl: saslauthd'
            'cyrus-sasl-plugins: authentication plugins other than sasldb')
source=(ftp://ftp.andrew.cmu.edu/pub/cyrus-mail/cyrus-sasl-${pkgver}.tar.gz
	      cyrus-sasl-2.1.19-checkpw.c.patch  
        db-4.7.patch
        sasl2_libtool-fix.patch
        01_fix_hurd_build)
options=('!makeflags')

build() {
  cd "${srcdir}/cyrus-sasl-${pkgver}"
  patch -Np0 -i ${srcdir}/cyrus-sasl-2.1.19-checkpw.c.patch 
  patch -Np0 -i ${srcdir}/db-4.7.patch 
  patch -p1 -i ${srcdir}/01_fix_hurd_build
  patch -p1 -i ${srcdir}/sasl2_libtool-fix.patch
  
  export LDFLAGS=""  

  ./configure --prefix= \
    	--sysconfdir=/etc \
    	--localstatedir=/var \
    	--disable-anon \
    	--disable-cram \
    	--disable-digest \
    	--disable-gssapi \
    	--enable-login \
    	--disable-otp \
    	--enable-plain \
        --mandir=/share/man 
  for dir in include lib sasldb plugins utils; do
    pushd ${dir} 
    make 
    popd
  done
}

package() {
  cd "${srcdir}/cyrus-sasl-${pkgver}"
  for dir in include lib sasldb plugins utils; do
    pushd ${dir} 
    make DESTDIR="${pkgdir}" install 
    popd
  done

  # install license
  install -D -m644 COPYING "${pkgdir}/share/licenses/${pkgname}/COPYING" 
}
md5sums=('2eb0e48106f0e9cd8001e654f267ecbc'
         'e27ddff076342e7a3041c4759817d04b'
         '71a3b7454f4d7cc2966b347bdf03f2fc'
         '8cad2a1b0d1b7cf396f167bc9e77b8f3'
         '0006d2aee9c4eb85509d89d4e705dc08')
