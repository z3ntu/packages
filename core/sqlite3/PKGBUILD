# Maintainer: Michael Walker <mike@barrucadu.co.uk>
# Maintainer: Stephen Gilles <stephendgilles@gmail.com>
# Contributor: Alexander Preisinger <alexander.preisinger@gmail.com>

pkgbase="sqlite3"
pkgname=('sqlite3' 'sqlite3-tcl' 'sqlite3-doc') 
_amalgamationver=3070800
_amalgamationver2=${_amalgamationver/00/}
_docver=${_amalgamationver}	#3070700
pkgver=${_amalgamationver2//0/.}
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('i686')
license=('custom')
url="http://www.sqlite.org/"
makedepends=('tcl' 'readline>=6.0.00')
source=(http://www.sqlite.org/sqlite-autoconf-$_amalgamationver.tar.gz
	http://www.sqlite.org/sqlite-doc-${_docver}.zip
	20-hurd-locking-style-amalgamated.patch
	license.txt)
options=('!libtool' '!emptydirs')

build() {
  export CFLAGS="$CFLAGS -DSQLITE_ENABLE_FTS3=1 -DSQLITE_ENABLE_COLUMN_METADATA=1 -DSQLITE_ENABLE_UNLOCK_NOTIFY -DSQLITE_SECURE_DELETE"

  # build sqlite
  cd "$srcdir"/sqlite-autoconf-$_amalgamationver
  
  # from Debian's patch tracker, modified for the amalgamated sqlite3
  patch -Np1 -i ${srcdir}/20-hurd-locking-style-amalgamated.patch	
  
  ./configure --prefix= \
	--disable-static
  make
  
  # build the tcl extension    
  cd "$srcdir"/sqlite-autoconf-$_amalgamationver/tea
  ./configure --prefix= \
	--with-system-sqlite
  make

}

package_sqlite3() {
 
 pkgdesc="A C library that implements an SQL database engine"
 depends=('readline>=6.0.00')
 conflicts=('sqlite')
 replaces=('sqlite')
 
  cd ${srcdir}/sqlite-autoconf-$_amalgamationver
  make DESTDIR=${pkgdir} install
  
  # license
  install -D -m644 ${srcdir}/license.txt ${pkgdir}/share/licenses/${pkgname}/license.txt
}

package_sqlite3-tcl() {
 
 pkgdesc="sqlite3 Tcl Extension Architecture (TEA)"
 depends=('sqlite3>=3.7.5')

  cd ${srcdir}/sqlite-autoconf-$_amalgamationver/tea
  make DESTDIR=${pkgdir} install
}

package_sqlite3-doc() {
 pkgdesc="most of the static HTML files that comprise this website, including all of the SQL Syntax and the C/C++ interface specs and other miscellaneous documentation"
 #arch=('any') - not yet supported

  #cd ${srcdir}/sqlite-doc-${_amalgamationver}
  cd ${srcdir}/sqlite-doc-${_docver}
  mkdir -p ${pkgdir}/share/doc/${pkgbase}
  cp -R *  ${pkgdir}/share/doc/${pkgbase}/
}

md5sums=('6bfb46d73caaa1bbbcd2b52184b6c542'
         '56c2e37c65075c612710ae1aba9703f2'
         '500fdcc07cbf8242da9457cb54af0ffe'
         'c1cdbc5544034d9012e421e75a5e4890')
