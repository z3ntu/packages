# Maintainer: Matthias Lanzinger <mlaenz@gmail.com>

pkgname=filesystem
pkgver=2010.04
pkgrel=4
pkgdesc="Base filesystem"
arch=('any')
license=('GPL')
url="http://www.archhurd.org"
groups=('base')
#depends=('sh' 'coreutils')
backup=(etc/fstab etc/group etc/hosts etc/passwd etc/shadow
        etc/gshadow etc/resolv.conf etc/shells etc/profile)
source=(fstab group hosts passwd shadow gshadow resolv.conf shells profile)
md5sums=('a1afa5704722bcac4794de89385b599b'
         '75c7e1770305e1f3b75c52785c137611'
         '81b3cb42a6ddabc2ed2310511ee9c859'
         '8a9042a2cedf6b6b47eb8973f14289cb'
         '019e5c24f9befef395a28e7ef2e4e5b9'
         '5340b9287b71ffe9d4d99510bffe933f'
         '3303543320ec010f1695f23b708a3693'
         '40dac0de4c6b99c8ca97effbd7527c84'
         'e51181c78339df07ccb4555ec6f3116d')


build()
{
  cd ${pkgdir}
  mkdir -p bin boot dev etc home hurd lib libexec mnt root sbin servers tmp var opt srv
  
  mkdir -p share/man/man{1,2,3,4,5,6,7,8}

  mkdir -p servers/socket
  touch servers/exec

  mkdir -p var/{cache/man,local,lock,opt,run,spool/mail,tmp,games}
  chmod 1777 var/lock
  mkdir -p var/log/old
  mkdir -p etc/{skel,profile.d}
  mkdir -p var/lib/misc
  (cd ${pkgdir}/var; ln -s spool/mail mail)

  # vsftpd won't run with write perms on /srv/ftp
  mkdir -p srv/ftp
  chown root.ftp srv/ftp
  chmod 555 srv/ftp

  install -d -o root -g root -m 755 srv/http

  chmod 1777 var/spool/mail tmp var/tmp
  chmod 0750 root

  #Allow setgid games to write scores:
  chmod 775 ${pkgdir}/var/games
  chown root:50 ${pkgdir}/var/games

  cd ${srcdir}
  cp fstab group hosts passwd shadow resolv.conf profile shells ${pkgdir}/etc/
  install -m 600 ${srcdir}/gshadow ${pkgdir}/etc/gshadow
  chmod 600 ${pkgdir}/etc/shadow

  # Prevent pacman from removing directory (Arch Linux FS#16886)
  mkdir ${pkgdir}/var/empty
  touch ${pkgdir}/var/empty/.keep

  # :(
  ln -s / ${pkgdir}/usr

}

