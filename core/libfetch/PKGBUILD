# Maintainer: Matthias Lanzinger <mlaenz@gmail.com>
#Thanks to Aaron Griffin, this is mostly his ArchLinux PKGBUILD

pkgname=libfetch
pkgver=2.32
pkgrel=1
pkgdesc="URL based download library"
arch=(i686)
license=('BSD')
depends=('openssl>=1.0.0')
url="http://cvsweb.netbsd.org/bsdweb.cgi/pkgsrc/net/libfetch/"
source=($pkgname-$pkgver.tar.gz
        Makefile
        libfetch_hurd.patch)
md5sums=('f7918e221f8e19494e3285999a827ee1'
         '298c43d709fd93457d85a1cf7428195a'
         '99cff9c19a6bb4e63610970f5dcdc9a6')

# source PKGBUILD && mksource
mksource() {
  export CVSROOT=:pserver:anoncvs@anoncvs.NetBSD.org:/cvsroot
  D=pkgsrc/net/libfetch
  [ -d "$D" ] && cvs up "$D" || cvs co "$D"
  pushd "$D"
  dirname=$(sed -n 's/DISTNAME=.*\(libfetch-.*$\)/\1/p' Makefile)
  cp -r files $dirname
  tar -czv --exclude=CVS -f ../../../$dirname.tar.gz $dirname
  rm -r $dirname
  popd
}

build() {
  cd $srcdir/$pkgname-$pkgver
  cp ../Makefile .
  patch -p2 < $srcdir/libfetch_hurd.patch
  make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}

