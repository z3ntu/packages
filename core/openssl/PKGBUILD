# Maintainer: Matthias Lanzinger <mlaenz@gmail.com>
# Thanks to Pierre Schmitz, this is mostly his archlinux PKGBUILD

pkgname=openssl
pkgver=0.9.8m
pkgrel=1
pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security"
arch=(i686)
url="http://www.openssl.org/"
license=('custom:BSD')
depends=('glibc')
mkdepends=('perl')
options=('!makeflags')
backup=('etc/ssl/openssl.cnf')
source=("http://openssl.org/source/$pkgname-$pkgver.tar.gz"
        'openssl-0.9.8l-fix_manpages-1.patch'
        'openssl-0.9.8a-no-rpath.patch'
        'openssl-0.9.8m-ca-dir.patch'
        'openssl-0.9.8m-cfb.patch'
        'openssl-0.9.8m-hurd-target.patch')

md5sums=('898bf125370926d5f692a2201124f8ec'
         '04a6a88c2ee4badd4f8649792b73eaf3'
         '4738d1eaff4e9975e9a26e4f76e48318'
         '1216688f46de6948ed2175e58e75c043'
         'd7ead9cc459e5d0233d5b1e1d24380a7'
         '89929eccdfb7171bece9d105d0b6ed00')


build() {
    cd $srcdir/$pkgname-$pkgver
    
     # avoid conflicts with other man pages: http://www.linuxfromscratch.org/patches/blfs/svn/
     patch -p1 -i $srcdir/openssl-0.9.8l-fix_manpages-1.patch
     # remove rpath: http://bugs.archlinux.org/task/14367
     patch -p1 -i $srcdir/openssl-0.9.8a-no-rpath.patch
     # set ca dir to /etc/ssl by default
     patch -p1 -i $srcdir/openssl-0.9.8m-ca-dir.patch
     # fix target settings for hurd
     patch -p1 -i $srcdir/openssl-0.9.8m-hurd-target.patch
    
    ./Configure hurd-x86 shared\
        --install-prefix=$pkgdir --prefix= --openssldir=/etc/ssl \
        -Wa,--noexecstack
        
    # see http://bugs.archlinux.org/task/18564
    # and http://cvs.openssl.org/chngview?cn=19336
    # will be in 0.9.8n
    patch -p0 -i $srcdir/openssl-0.9.8m-cfb.patch || return 1

    make || return 1
}

package() {
    cd $srcdir/$pkgname-$pkgver
    make MANDIR=/share/man INSTALLTOP=/ install || return 1
    install -D -m644 LICENSE $pkgdir/share/licenses/$pkgname/LICENSE
}

