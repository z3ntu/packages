# Maintainer: Matthias Lanzinger <mlaenz@gmail.com>
# Thanks to Pierre Schmitz, this is mostly his archlinux PKGBUILD

pkgname=openssl
_ver=1.0.0a
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
pkgrel=1
pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security"
arch=(i686)
url="http://www.openssl.org/"
license=('custom:BSD')
depends=('glibc')
mkdepends=('perl')
options=('!makeflags')
backup=('etc/ssl/openssl.cnf')
source=("http://openssl.org/source/$pkgname-${_ver}.tar.gz"
        'fix-manpages.patch'
        'no-rpath.patch'
        'ca-dir.patch'
        'hurd-target.patch')

build() {
    cd $srcdir/$pkgname-${_ver}
    
     # avoid conflicts with other man pages: http://www.linuxfromscratch.org/patches/blfs/svn/
     patch -p0 -i $srcdir/fix-manpages.patch
     # remove rpath: http://bugs.archlinux.org/task/14367
     patch -p0 -i $srcdir/no-rpath.patch
     # set ca dir to /etc/ssl by default
     patch -p0 -i $srcdir/ca-dir.patch
     # fix target settings for hurd
     patch -p1 -i $srcdir/hurd-target.patch
    
    ./Configure hurd-x86 shared\
        --install-prefix=$pkgdir --prefix= --openssldir=/etc/ssl \
        --libdir=lib zlib enable-md2 -Wa,--noexecstack
        
    make 
}

package() {
    cd $srcdir/$pkgname-${_ver}
    make MANDIR=/share/man INSTALLTOP=/ install 
    install -D -m644 LICENSE $pkgdir/share/licenses/$pkgname/LICENSE
}

md5sums=('e3873edfffc783624cfbdb65e2249cbd'
         'f540cd9e0e3047d589d0581fe7a2d0f2'
         'dc78d3d06baffc16217519242ce92478'
         '3bf51be3a1bbd262be46dc619f92aa90'
         '599b06153165eab7913e7d8e1d04caf9')
