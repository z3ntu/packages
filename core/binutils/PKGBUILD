# Maintainer: Matthias Lanzinger <mlaenz@gmail.com>

pkgname=binutils
pkgver=2.20.1
pkgrel=2
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=(i686)
url="http://www.gnu.org/software/binutils/"
license=('GPL3')
groups=('base')
depends=('glibc' 'zlib')
install=binutils.install
source=(ftp://ftp.gnu.org/pub/gnu/binutils/$pkgname-$pkgver.tar.bz2)
md5sums=('9cdfb9d6ec0578c166d3beae5e15c4e5')

build() {
  cd $srcdir
  rm -rf build
  mkdir build
  cd build

  ../$pkgname-$pkgver/configure --prefix= \
              --disable-multilib --enable-shared \
              --mandir=/share/man --infodir=/share/info

  make configure-host
  make tooldir=${pkgdir}
}

package() {
  cd $srcdir/build

  make prefix=${pkgdir} tooldir=${pkgdir} install
  install -m644 ${srcdir}/${pkgname}-${pkgver}/include/libiberty.h ${pkgdir}/include

  # Rebuild libiberty.a with -fPIC
  make -C libiberty clean
  make CFLAGS="$CFLAGS -fPIC" -C libiberty
  install -m644 libiberty/libiberty.a ${pkgdir}/lib

  # Rebuild libbfd.a with -fPIC
  make -C bfd clean
  make CFLAGS="$CFLAGS -fPIC" -C bfd
  install -m644 bfd/libbfd.a ${pkgdir}/lib

  rm -f ${pkgdir}/share/man/man1/{dlltool,nlmconv,windres,windmc}*
}

