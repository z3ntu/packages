# Maintainer: Matthias Lanzinger <mlaenz@gmail.com>

pkgname=binutils
pkgver=2.20.1
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=(i686)
url="http://www.gnu.org/software/binutils/"
license=('GPL3')
groups=('base')
depends=('glibc' 'zlib')
install=binutils.install
source=(ftp://ftp.gnu.org/pub/gnu/binutils/$pkgname-$pkgver.tar.bz2)
md5sums=('9cdfb9d6ec0578c166d3beae5e15c4e5')

build() {
  cd $srcdir
  rm -rf build
  mkdir build
  cd build

  ../$pkgname-$pkgver/configure --host=i686-pc-gnu --prefix= \
              --disable-multilib --enable-shared \
              --mandir=/share/man --infodir=/share/info 

  make configure-host || return 1
  make || return 1
}

package() {
  cd $srcdir/build
  
  make DESTDIR="$pkgdir/" install
  rm -rf $pkgdir/i686-pc-gnu #only when cross-compiling?
}

