# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>

#
### Attention: remove old pkg before building - it links against itself! ###
#

pkgname=heimdal
pkgver=1.3.3
pkgrel=1
pkgdesc="Implementation of Kerberos V5 libraries"
arch=('i686')
url="http://www.h5l.org/"
license=('custom')
depends=('db' 'openssl' 'sqlite3' 'e2fsprogs')
backup=(etc/krb5.conf)
options=('!libtool' '!emptydirs' '!zipman')
install=heimdal.install
source=(http://www.h5l.org/dist/src/${pkgname}-${pkgver}.tar.gz
	001_all_heimdal-no_libedit.patch
	002_all_heimal-fPIC.patch
	003_all_heimdal-rxapps.patch
	005_all_heimdal-suid_fix.patch
	012_all_heimdal-berkdb.patch
	013_all_heimdal-pthread-lib.patch
	014_all_heimdal-path.patch
  015_all_heimdal-hurd_ioctl.patch
	heimdal-kdc.rc
	kadmind.rc
	kpasswd.rc)

build() {
  cd ${srcdir}/heimdal-${pkgver}
  patch -Np0 -i ${srcdir}/001_all_heimdal-no_libedit.patch 
  patch -Np0 -i ${srcdir}/002_all_heimal-fPIC.patch 
  patch -Np0 -i ${srcdir}/003_all_heimdal-rxapps.patch 
  patch -Np0 -i ${srcdir}/005_all_heimdal-suid_fix.patch 
  patch -Np1 -i ${srcdir}/012_all_heimdal-berkdb.patch 
  patch -Np1 -i ${srcdir}/013_all_heimdal-pthread-lib.patch 
  patch -Np0 -i ${srcdir}/014_all_heimdal-path.patch 
  patch -Np1 -i ${srcdir}/015_all_heimdal-hurd_ioctl.patch

  sed -i -e 's|var/heimdal|var/lib/heimdal|g' configure.in \
	doc/setup.texi doc/heimdal.info kadmin/kadmind.8 kdc/kdc.8 \
	lib/hdb/hdb.h lib/krb5/krb5.conf.5 lib/krb5/krb5.conf.cat5

  sed "s|MAXHOSTNAMELEN|1024|g" -i ./**/**/**/*.c
  sed "s|PATH_MAX|4096|g" -i ./**/**/*.c 
 
  libtoolize --force 
  aclocal -I cf 
  autoconf 
  automake 

  ./configure --prefix= --enable-shared=yes --without-x \
	--sysconfdir=/etc --mandir=/share/man \
	--datadir=/var/lib/heimdal \
	--localstatedir=/var/lib/heimdal \
	--with-openssl= \
	--with-readline-lib=/lib \
	--with-readline-include=/include/readline \
	--with-sqlite3-lib=/lib \
	--with-sqlite3-include=/include \
  --disable-afs-support	--libexecdir=/sbin
	
  make 
}

package() {
  cd ${srcdir}/heimdal-${pkgver}

  make DESTDIR=${pkgdir} install 

  # Rename daemons and their manpages
  for i in telnetd ftpd rshd; do
    mv ${pkgdir}/share/man/man8/{,k}${i}.8 
    mv ${pkgdir}/sbin/{,k}${i} 
  done
  
  # Rename clients and their manpages
  for i in rcp rsh telnet ftp su login; do
    if [ -f ${pkgdir}/share/man/man1/${i}.1 ]; then
      mv ${pkgdir}/share/man/man1/{,k}${i}.1 
    fi
    mv ${pkgdir}/bin/{,k}${i} 
  done
  rm -rf ${pkgdir}/share/man/cat{1,3,5,8}
  
  # Arch could be a KDC too
  install -d ${pkgdir}/etc/rc.d
  install -m644 ${srcdir}/heimdal-${pkgver}/krb5.conf ${pkgdir}/etc/ 
  for i in heimdal-kdc kadmind kpasswd; do
    install -m755 ${srcdir}/${i}.rc ${pkgdir}/etc/rc.d/${i} 
  done

  # Remove conflicts
  rm ${pkgdir}/share/man/man5/ftpusers.5*		          # man-pages
  rm ${pkgdir}/share/man/man3/{DES,DH,EVP,OpenSSL,RAND,RSA}*  # openssl (a bit overzealous...)
  rm ${pkgdir}/share/man/man3/os.3*                           # erlang

  # Install the license
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE \
  	${pkgdir}/share/licenses/${pkgname}/LICENSE 
}
md5sums=('963c09f1b14c41660be70b55fae9f163'
         '98e28f11f906c967aac22d6184102c9e'
         '6d5571bdedba2e2423b90bccdbac2c0a'
         '2feec3924ee5230b54175b4d4000c872'
         '45aeb207f360f9f4e9e0fabc8bfeecbc'
         '56f5d10d0ec40f2fda82ef144ffac1e0'
         '1b8665b771c4eb6b56ea8582c96e56e3'
         '30575adf685dcfe4401b3f73dc73df93'
         '722dac5170db541ec5183a7eece36b82'
         '5682b79cb93fd71fcbb0ff40654e698c'
         '8d452c13a3e3253f767b93a460a32c2b'
         'b9142d1a7ceab54f4bbea76d42a6dc68')
